OpenSakuraMenu
{
	if AutoUpdates[1] == 2 //If an update is available, the user will be notified when they try to open the menu
	{
		"\0\s[2]\i[35](It looks like there's a new update! Do you want to download it?)"
		--
		//Note: the download it now option will go straight into updating without directing to another function. You can do this with some things using 'script:' in the function name, but it's finicky. If this gives you issues feel free to replace it.
		all:{
		"\_q\n\n"
		"\![*]\q[Download it now,script:\![updatebymyself\]]\n"
		"\![*]\q[Maybe later,UpdateLater]\n\n"
		"\![*]\q[Turn off auto updates,UpdateOff]\e"
		}
	}
	else //Normal menu opening
	{
		"\0\b2"
		--
		"\s[1]\i[1]\i[80]\i[35]"
		--
		"\_q(She picks her head up to look at you.)\w8\w6\_q"
		"\_q(Looks like you have her attention.)\w8\w6\_q"
		--
		OnMenuRun
	}
}

Select.UpdateLater //If the user decides to update later
{
	AutoUpdates[1] = 1
	
	"\0(Reminders for updates will pop up once a day!)\e"
}

Select.UpdateOff //If the user no longer wants to be notified about updates
{
	AutoUpdates[0] = "Off"
	AutoUpdates[1] = 1
	
	"\0(You can turn auto updates back on in her config menu.)\e"
}

OnMenuRun : all //Main menu. The all modifier here means every string will be output in order, so you don't need to write -- between lines
{
	"\b2\n\n\![set,autoscroll,disable]\_q"
	
	"\![close,inputbox,__SYSTEM_ALL_INPUT__]" //to remove input box from debug stat menu? remove if needed
	
	"(You ask her...)\n"
	if lastTalk != "" {"\![*]\q[To repeat what she said,OnLastTalk]"}
	else {"\f[color,gray]\![*]To repeat what she said\f[color,default]"}
	"\n"
	"\![*]To talk \_a[OnAiTalk,userprompt]to you\_a"
		" or \_a[OnAiTalk,holler]her friends\_a\n"
	"\![*]To talk to \_a[OnStartTalking]another ghost\_a\n"
	"\![*]\q[To do something for you,OnFunctionMenu]\n"
	"\![*]\q[Some questions,OnQuestionMenu]\n\n"
	
	"(Or you could...)\n"
	"\![*]\q[Interact with her,OnInteractMenu]\n"
	"\![*]\q[Open the config menu,OnConfigMenu]\n"
	"\![*]\q[Check your stats,OnStatMenu,]"
	
	
	"\n\n\f[align,right]\q[Nevermind,CANCEL] \![*]"
	"\n\n[half]\f[align,default]"
	"Runtime: %(runtime)%(newline)\f[align,right]Version %(ghostver)"
	
	if debug //%(debug = 1) in script input to show the debug options
	{
		"\n\n[half]\f[align,center]"
		"\![*]\q[Test Variable,OnTestVar]  "
		"\![*]\q[To-do list,OnTemplateTodo]"
	}
	
	"\e"
}

OnLastTalk //Repeats the last thing the ghost said
{
	"%(lastTalk)\e"
}

//----------Question Menu----------
OnQuestionMenu : all
{
	"\0\_q"
	
	"\n[half]\f[align,center](You ask her about...)\n\n\f[align,default]"
	
	"\![*]\q[Herself,AboutVera]\n" 				//hobbies, interests, life, other stuff
	"\![*]\q[How she's doing,CheckIn]\n" 		//tiredness checker + thoughts on recent events/flags
	"\![*]\q[People she knows,OnPeopleMenu]\n" 	//family, friends, enemies, acquaintances
	"\![*]\q[Dreams & sleeping,DreamMenu]\n" 	//dream menu, talk abt horrorterrors and dreamselves and all that
	"\![*]\q[Yourself,AboutYou]\n" 				//what she thinks of you based on stats and flags and stuff
	"\![*]\q[Your computer,SnoopingOoh]\n" 		//eventual snoop function hehehehehe
	
	"\n\f[align,right]\![*]\q[Back to Main Menu,OnMenuRun]  \![*]\q[Cancel,CANCEL]\e"
}

Select.AboutVera : nonoverlap_pool
{
	//general: life, feelings, aspiration, sylladex, strife deck, past
	//hobbies: origami, candles, aromatherapy, travelling
	//you can ask her to play with her sylladex and origami and stuff in the interaction menu
	
	if posfeel >= 25 //starting to see u like a friend
	{
		posfeel += 0.5
		"\0\s[2]\i[41]\_q%(vera)\_q you know i use the \w6\1\s[1050]\0\s[1]\i[41]origami \w6fetch modus by now, \w6right..? \w8\w4\n\s[3]\i[19]\i[41]\i[26]\_q%(vera)\_q i've had it so long i don't remember where i got it, \w2but it's pretty neat. \w8\w4\n\s[2]\i[84]\_q%(vera)\_q if i ever wanna pull something from my sylladex, \w2i have to fold a paper approximation of it... \w8\w4\n\1\s[-1]\0\s[1]\i[85]\i[17]\_q%(vera)\_q it can get kinda finnicky."
		"\0\s[2]\i[18]\_q%(vera)\_q i really like pillowkind as my kind abstratus... \w8\w8\n\s[3]\_q%(vera)\_q i've been able to allocate even slightly ""pillowy"" things to my strife deck, \w2\i[19]like plushies and stuff..!! \w8\w8\n\i[26]\_q%(vera)\_q um, \w6i know that sounds useless, \w6but-! \w8\w8\n\s[5]\i[27]\i[1]\i[80]\_q%(vera)\_q ...agh, it's hard to explain!!"
		"\0\s[1]\_q%(vera)\_q oh, \w2did i tell you that i like making candles..? \w8\w8\n\s[2]\_q%(vera)\_q um, \w6not very well, \w6\i[26]because we don't really have money for, like, supplies? \w8\w8\n\s[3]\i[19]\i[27]\_q%(vera)\_q but \w2i just think they're neat..!"
		"\0\s[2]\_q%(vera)\_q ...my perscription is really outdated. \w8\w6\n\s[3]\i[19]\i[26]\_q%(vera)\_q but then again, \w6most of the things we have are..."
		"\0\s[1]\i[18]\_q%(vera)\_q i live in a nice apartment with a balcony... \w8\w8\n\s[3]\i[26]\_q%(vera)\_q it's nice and cozy, \w2and we have everything we need in it."
		"\0\s[2]\i[41]\_q%(vera)\_q i forget if i told you this, but \w8\w8\n\s[3]\_q%(vera)\_q i like this shirt cause the rest of my friends have street sign shirts too \:\)"
		if posfeel >= 70
		{
			"\0\s[2]\i[17]\i[41]\_q%(vera)\_q mm, \w2random thought, \w2but \w8\w2\n\s[1]\i[18]\i[41]\i[27]\_q%(vera)\_q i forget if i got my origami fetch modus BECAUSE i liked origami, \w8\w8\n\s[2]\i[17]\i[41]\i[27]\_q%(vera)\_q or if having the modus made me like origami in the first place..."
			"\0\s[2]\i[1]\i[80]\i[17]\_q%(vera)\_q i really want to travel and escape my apartment one day... \w8\w4\n\s[1]\i[1]\i[80]\i[26]\_q%(vera)\_q all the travel blogs make the world look so \w2beautiful. \w8\w8\w8\n\s[11]\i[1]\i[10]\_q%(vera)\_q ...i want to visit my friends, too."
			"\0\_q%(vera)\_q i don't... \w8\w8\n\s[5]\i[1]\i[80]\![anim,clear,25]\_q%(vera)\_q i don't go to school much anymore. \w8\w8\w2\n\_q%(vera)\_q i just skip and stay home. \w8\w8\w2\n\_q%(vera)\_q it started all cause dad stopped being around, \w8and i kept losing track of time by falling asleep, \w8so..."
			"\0\s[5]\i[62]\_q%(vera)\_q ...i have tons of irritation in my left eye. \w8\w6\n\s[1]\_q%(vera)\_q it's my weak eye, \w6basically. \w8\w6\n\s[2]\i[26]\_q%(vera)\_q i don't really know why the irritation is a thing, \w6but it's not like we have the money to check it%(br)out..."
		}
	}
	else //not negative route, not friendly enough yet
	{
		posfeel += 0.5
		"\0\s[1]\i[1]\i[80]\_q%(vera)\_q ...oh, \w6um, \w8\w8\n\s[2]\i[41]\i[1]\i[26]\_q%(vera)\_q i'm tired \w6and forget stuff \w4a%(br)lot... \w8\w8\n\s[3]\i[1]\i[26]\_q%(vera)\_q i'm trying my best, \w4though..!"
		"\0\s[1]\i[1]\i[80]\_q%(vera)\_q ...ah, \w6my name is vera \w6\i[26]thandi..? \w8\w8\n\s[2]\i[17]\i[1]\i[80]\i[26]\_q%(vera)\_q i don't really know what else there is to say..."
		"\0\s[1]\i[1]\i[80]\_q%(vera)\_q i'm 14 years old..! \w8\w8\n\s[3]\i[18]\i[1]\i[80]\_q%(vera)\_q put my name in the naming box and everything a long time ago, \w6heheh."
		"\0\s[1]\i[1]\_q%(vera)\_q ...the thing on my shirt? \w8\w8\n\s[2]\i[1]\_q%(vera)\_q there's no meaning to it or anything, \w6i've just had it for a long time and i really like it..."
		if posfeel >= 5 //stuff i dont want user to see until vera at least knows them a lil bit YKYK
		{
			"\0\s[1]\i[1]\i[80]\_q%(vera)\_q um, \w4i use an \w6\1\s[1050]\0origami \w6fetch%(br)modus... \w8\w4\n\s[2]\i[1]\i[84]\_q%(vera)\_q it's not that complicated, \w6\![anim,clear,84]\i[85]i just need to make origami of what i want to get out of my sylladex... \w8\w4\n\1\s[-1]\0\s[1]\i[1]\i[80]\i[18]\_q%(vera)\_q i like it."
			"\0\s[3]\i[26]\i[1]\i[80]\_q%(vera)\_q ah, \w6my current kind abstratus is kinda weird... \w8\w8\n\i[19]\_q%(vera)\_q it's, \w6um, \w6pillowkind. \w8\w8\n\s[4]\i[17]\i[26]\i[1]\i[80]\_q%(vera)\_q it looks dumb in a strife, \w6but it's gotten pretty useful for me..."
		}
	}
	--
	"%(backtoq)"
}

Select.CheckIn
{
	"\0(Placeholder for check ins!! gasp)"
	--
	"%(backtoq)"
}

OnPeopleMenu : all
{
	"\0\_q\n\f[align,center]"
	
	"\n[half](GENERAL)\n"
	"\![*]\q[Family,genfamily]  "
	"\![*]\q[Friends,genfriend]  "
	"\![*]\q[Others,genother]\n"
	
	"\n(SPECIFIC)\n"
	"\f[height,-2]\f[color,gray](Click arrow by name to see their original profiles)\f[height,default]\f[color,default]\n"
	if KnowAbout[0] != 0 {"\_a[AUTO_ANCHOR:dad]\![*]\_a\q[Her dad,abtluce]  "} else {"\![*]--- ---  "}
	if KnowAbout[1] != 0 {"\_a[AUTO_ANCHOR:mom]\![*]\_a\q[Her mom,abtmom]\n"} else {"\![*]--- ---\n"}
	if KnowAbout[2] != 0 {"\_a[AUTO_ANCHOR:skul]\![*]\_a\q[Skul,abtskul]  "} else {"\![*]----  "}
	if KnowAbout[3] != 0 {"\_a[AUTO_ANCHOR:ulys]\![*]\_a\q[Ulys,abtulys]  "} else {"\![*]----  "}
	if KnowAbout[4] != 0 {"\_a[AUTO_ANCHOR:lila]\![*]\_a\q[Lila,abtlila]"} else {"\![*]----"}
	
	"\n \n\f[align,right]\![*]\q[Back,OnQuestionMenu]  \![*]\q[Cancel,CANCEL]\e"
}

Select.genfamily : nonoverlap_pool
{
	if posfeel >= 70
	{
		posfeel += 0.5
		
		"\0\s[2]\i[17]\i[1]\_q%(vera)\_q ...i don't get the people who think ""blood is thicker than water"" and all that stuff. \w8\w8\n\s[1]\i[1]\_q%(vera)\_q i mean, \w4i like my @dad- \w8\w2\s[2]\i[1]\i[41]and my @mom, \w4even though i dunno who she is- \w8\w2\s[1]\i[1]\i[80]but my friends have been around for me more than them, \w4so... \w8\w8\n\s[2]\i[1]\i[80]\i[17]\_q%(vera)\_q but my family provides for me, \w4so i should be grateful, \w6i think... \w8\w8\n\s[3]\i[19]\i[26]\i[1]\_q%(vera)\_q um, \w4it's hard to say my feelings about it accurately in words, \w6so..."
		"\0\s[1]\i[27]\_q%(vera)\_q ...they're important to me, \w2and i mean that, \w2genuinely. \w8\w8\n\s[2]\i[1]\i[80]\i[17]\_q%(vera)\_q but at the same time, \w6i want them to be more, like... \w8\w8\w8\n\i[26]\_q%(vera)\_q am i selfish for feeling this way..?"
	}
	elseif posfeel >= 20 //starting to see u like a friend
	{
		"\0\s[1]\i[1]\i[80]\_q%(vera)\_q ...i really am grateful for my family. \w8\w8\i[18]\i[27]\n\_q%(vera)\_q even if it doesn't seem like it. \w8\w8\n\s[2]\i[1]\i[80]\i[26]\i[17]\_q%(vera)\_q \i get to have a tablet because my @dad used a lot of money to buy it, \w6and i have a nice room with everything i want..."
		"\0\s[3]\i[26]\_q%(vera)\_q ...what do you want to know about them? \w8\w8\n\s[4]\i[17]\i[26]\_q%(vera)\_q i really don't have much to say about it. \w8\w8\n\![anim,stop,17]\i[18]\_q%(vera)\_q i like my family."
		"\0\s[2]\i[1]\i[80]\_q%(vera)\_q ...yeah, \w2i have a family. \w8\w8\n\s[5]\i[1]\i[80]\![anim,stop,25]\_q%(vera)\_q i know that's more than some people have, \w2so..."
	}
	else //not negative route, not friendly enough yet
	{
		"\0\s[1]\i[1]\i[80]\i[26]\_q%(vera)\_q um... \w8\w8\n\_q%(vera)\_q what do you mean..? \w8\w8\n\s[2]\i[1]\i[80]\_q%(vera)\_q i have a family, \w6yeah..."
		"\0\s[1]\i[1]\i[80]\_q%(vera)\_q um, \w6i live with my @dad, \w6since i'm still a kid and all..."
		"\0\s[1]\i[1]\i[80]\_q%(vera)\_q my \w4family... \w8\w8\n\i[18]\_q%(vera)\_q i'm really grateful for the family i do have... \w8\w8\n\s[2]\i[17]\i[1]\i[80]\_q%(vera)\_q i'd probably \w4be worse off without him..."
		"\0\s[2]\i[1]\i[80]\_q%(vera)\_q ...yeah, \w2i have a family. \w8\w8\n\_q%(vera)\_q ...what else do i need to say about it?"
	}
	--
	" \n\n\f[color,default]\![*]\q[Ask something else,OnPeopleMenu]\n\![*]\q[I'm done,CANCEL]\e"
}

Select.genfriend : nonoverlap_pool
{
	if posfeel >= 20 //starting to see u like a friend
	{
		posfeel += 0.5
		
		"\0\s[1]\i[1030]\_q%(vera)\_q ah, \w4my friend group is... \w8\w8\n\i[18]\_q%(vera)\_q i think it's the perfect size. \w8\w8\n\s[3]\i[26]\i[1030]\_q%(vera)\_q having too many people around overwhelms me, y'know?"
		"\0\s[1]\_q%(vera)\_q even though we've been friends for years, \w4i don't actually know what they all look like... \w8\w8\n\_q%(vera)\_q a certain someone is really uptight about ""privacy"", so..."
		"\0\s[1]\_q%(vera)\_q ...um, \w6have you heard me refer to my friend group as... \w2\w8\w8\n\_q%(vera)\_q the... \w8street sign kids, \w8or \w8whatever..? \w8\w8\n\_q%(vera)\_q ah, \w2geez, \w2it's just something one of my friends came up with, \w2but to be fair..! \w8\w8\n\_q%(vera)\_q she wasn't wrong, \w2'cause we all have street sign shirts..!"
		if posfeel >= 70
		{
			"\0\s[2]\_q%(vera)\_q ...we've been throwing around the idea of meeting up, \w8\w8\n\s[3]\i[26]\_q%(vera)\_q but, \w2uh, \w2there's so many reasons we can't... \w8\w8\n\s[4]\i[17]\i[26]\_q%(vera)\_q @dad wouldn't let me, \w4we're all 14, \w4@ulys and @lila live waaay out in alaska, \w4@skul will NOT tell us anything about himself- \w2\w8\w8\n\s[3]\i[19]\i[27]\_q%(vera)\_q um, \w6it makes it VERY hard to take the idea seriously right now..!"
		}
	}
	else //not negative route, not friendly enough yet
	{
		posfeel += 0.5
		
		"\0\s[1]\i[1]\i[80]\i[27]\_q%(vera)\_q um- \w6i have friends!! \w8\w8\n\s[5]\i[27]\i[1]\i[80]\_q%(vera)\_q SO \w2many friends. \w8\w8\n\s[1]\i[18]\i[1]\i[80]\_q%(vera)\_q you'd be jealous."
		"\0\s[1]\i[1]\i[80]\_q%(vera)\_q ...oh, \w6um! \w8\w8\n\i[27]\i[18]\_q%(vera)\_q my friends are awesome! \w8\n\_q%(vera)\_q we talk about ghosts and stuff together!"
		{
			"\0\s[3]\i[1]\i[80]\i[27\i[18]\_q%(vera)\_q um, \w6my friends are really cool and talented, \w2like..! \w8\w8\n"
			--
			"\s[1]\i[1]\i[80]\i[18]\_q%(vera)\_q one of them is REALLY good at coding and stuff!! \w8\w8\n\i[1030]\i[27]\_q%(vera)\_q he's so confident and cool..!"
			"\s[3]\i[1]\i[80]\i[19]\_q%(vera)\_q one of them is a very eloquent writer, and they make the best cakes..."
			"\s[1]\i[1]\i[80]\i[27]\_q%(vera)\_q one of them has a weirdly good ability to make people look nice..!! \w8\n\s[2]\i[27]\i[41]\i[1]\i[61]\i[17]\_q%(vera)\_q like, \w2um, \w2make-up and stuff..!"
		}
	}
	--
	" \n\n\f[color,default]\![*]\q[Ask something else,OnPeopleMenu]\n\![*]\q[I'm done,CANCEL]\e"
}

Select.genother
{
	if posfeel >= 20 //starting to see u like a friend
	{
		posfeel += 0.5
		
		"\0\s[2]\i[17]\i[26]\_q%(vera)\_q ...i don't see my neighbors very often, \w4but i'm kinda scared about what they think of me... \w8\w8\n\s[5]\i[26]\_q%(vera)\_q i can't tell what the looks on their face mean when they see me."
		"\0\s[2]\i[1]\i[80]\_q%(vera)\_q there's this really nice lady who lives in the apartment directly above us... \w8\w8\n\i[18]\_q%(vera)\_q she always talks about her art whenever we have the chance to talk. \w8\w8\n\s[4]\i[1]\i[80]\i[17]\_q%(vera)\_q it's nice..."
		"\0\_q%(vera)\_q i don't really have a problem with any of my teachers... \w8\w8\n\_q%(vera)\_q i just wish they'd stop giving out so much work."
		
		if posfeel >= 70
		{
			
		}
	}
	else //not negative route, not friendly enough yet
	{
		"\0\s[1]\i[1]\i[80]\i[26]\_q%(vera)\_q ...do i need to tell you about the people i know? \w8\w8\n\s[2]\i[17]\i[1]\i[80]\i[26]\_q%(vera)\_q i-i don't mean to be a jerk, \w2but i don't know how to answer that, \w2%(pername)..."
		"\0\s[1]\i[1]\i[80]\i[18]\_q%(vera)\_q ...i get along with the people around me, \w2i guess. \w8\w8\n\s[1]\i[1]\i[80]\i[26]\_q%(vera)\_q um, \w2sorry, \w2i don't really know what you're asking..."
		"\0\s[1]\i[1]\i[80]\_q%(vera)\_q ...other than friends and family?\w8\w8\w8\n\s[2]\i[1]\i[41]\i[61]\_q%(vera)\_q ...hm. \w8\w8\n\i[17]\i[26]\_q%(vera)\_q i don't feel like telling you that... \w8\w8\s[1]\i[1]\i[80]\i[26]\i[18]s\w2-\w2sorry."
	}
	--
	" \n\n\f[color,default]\![*]\q[Ask something else,OnPeopleMenu]\n\![*]\q[I'm done,CANCEL]\e"
}

Select.abtluce : nonoverlap_pool
{
	if posfeel >= 70
	{
		posfeel += 0.5
		
		
	}
	elseif posfeel >= 20 //starting to see u like a friend
	{
		
	}
	else //not negative route, not friendly enough yet
	{
		"\0\s[2]\i[1]\i[80]\_q%(vera)\_q ...uh, \w2yeah, \w6my dad. \w8\w8\n\s[5]\![anim,stop,25]\i[1]\i[80]\_q%(vera)\_q he's fine."
		"\0\s[1]\i[1]\i[80]\i[17]\_q%(vera)\_q ...what's YOUR dad like, %(pername)?"
	}
	--
	" \n\n\f[color,default]\![*]\q[Ask something else,OnPeopleMenu]\n\![*]\q[I'm done,CANCEL]\e"
}

Select.abtmom : nonoverlap_pool
{
	if posfeel >= 70
	{
		posfeel += 0.2
		
		"\0\s[2]\i[17]\_q%(vera)\_q ...is having a mom nice? \w8\w8\n\s[5]\![anim,stop,25]\_q%(vera)\_q would it be better than having..?"
		"\0\s[2]\_q%(vera)\_q i know @skul has a mom, \w4but she also... \w8\w8\n\i[17]\i[26]\_q%(vera)\_q seems... kinda like my @dad? \w8\w8\n\s[5]\i[26]\_q%(vera)\_q i mean, @skul doesn't really talk about it either, i shouldn't make assumptions..."
	}
	elseif posfeel >= 20 //starting to see u like a friend
	{
		
	}
	else //not negative route, not friendly enough yet
	{
		"\0\s[2]\i[1]\i[80]\i[17]\_q%(vera)\_q um, my mom is... \w8\w8\n\![anim,stop,17]\i[18]\_q%(vera)\_q hm. \w4i don't actually need to tell you."
		"\0\s[1]\i[1]\i[80]\i[18]\_q%(vera)\_q ...what else do you want to know about her? \w8\w8\n\s[1]\i[17]\i[1]\i[80]\_q%(vera)\_q it's not like i know more than you, %(pername)."
	}
	--
	" \n\n\f[color,default]\![*]\q[Ask something else,OnPeopleMenu]\n\![*]\q[I'm done,CANCEL]\e"
}

Select.abtskul : nonoverlap_pool
{
	//later add negfeel routes
	
	posfeel += 0.5
	
	"\0\s[3]\i[1]\i[80]\_q%(vera)\_q he's like, soooo good at coding! \w8\w8\n\s[2]\i[41]\_q%(vera)\_q i mean, i don't understand any of it, but he can make so many cool things.......... \w8\w8\n\_q%(vera)\_q you know?"
	"\0\s[1]\_q%(vera)\_q @skul takes my dreams really seriously when i tell him about them. \w8\w8\n\s[2]\i[41]\_q%(vera)\_q kinda like, prophetic dreams? \w8\w8\n\s[1]\_q%(vera)\_q i half believe him, but i think most of his sources for believing so are more, like, correlation \w4and NOT causation? \w8\w8\n\s[3]\i[19]\_q%(vera)\_q but it's interesting so i let him believe whatever :)"
	"\0\s[2]\_q%(vera)\_q he comes off a little mean sometimes. \w8\w8\n\i[18]\_q%(vera)\_q but ever since we pointed it out to him, \w4he's really been working on being more sensitive to others..."
	"\0\s[1]\_q%(vera)\_q it's nighttime during my afternoon wherever he's at. \w8\w8\w4\n\_q%(vera)\_q he's active then, \w4but i'm usually online at my night too, \w4so.. \w8\w8\w8\n\s[2]\i[1]\i[80]\_q%(vera)\_q um, \w4now i realize how messed up our sleeping schedules are..."
	"\0\s[2]\_q%(vera)\_q @skul was the one that got us all to use hollercaller. \w8\w8\n\s[1]\i[1]\i[80]\_q%(vera)\_q it's supposed to be super private \w4and open source \w6n he gave it to us through a direct .exe,, \w8\w8\n\s[3]\i[1]\i[18]\i[80]\i[1030]\_q%(vera)\_q it's very,, \w8yknow, \w8him :)"
	
	--
	" \n\n\f[color,default]\![*]\q[Ask something else,OnPeopleMenu]\n\![*]\q[I'm done,CANCEL]\e"
}

Select.abtulys : nonoverlap_pool
{
	posfeel += 0.5
	
	"\0\s[1]\_q%(vera)\_q the food and recipes they send in the hollerfeed look sooooo good! \w8\w8\n\s[3]\i[27]\i[19]\_q%(vera)\_q makes me wanna fly over to alaska no matter what..!!"
	"\0\s[2]\_q%(vera)\_q i like reading the short stories they put up on their website... \w8\w8\n\s[1]\_q%(vera)\_q they could make money off of that stuff, it's really good and thought-provoking!"
	"\0\s[3]\i[27]\_q%(vera)\_q they lecture us about being too reckless and stuff, but i KNOW they do just as much crime as we do!! \w8\w8\n\i[18]\_q%(vera)\_q just cause you don't get caught doesn't mean it doesn't count!"
	"\0\s[2]\i[41]\_q%(vera)\_q it's usually morning for them when it's my noon... \w8\w8\n\s[1]\_q%(vera)\_q like, just a few hours behind. \w8\w8\n\s[4]\i[17]\_q%(vera)\_q unlike @skul."
	"\0\s[2]\i[1]\i[80]\_q%(vera)\_q they're a really stabilizing presence to have around... \w8\w8\n\s[3]\i[1]\i[80]\i[18]\_q%(vera)\_q they know just what to say \w2and just how to word things... \w8\w8\n\s[4]\i[1]\i[80]\i[17]\_q%(vera)\_q um, \w4if you ignore \w6the sarcasm."
	
	{
	"\0\s[2]\i[1]\i[80]\_q%(vera)\_q ...they seem to live such a peaceful life. \w8\w8\n\_q%(vera)\_q like, a tiny village in the countryside surrounded by plants and animals,, "
	--
	if posfeel >= 50; "\w8\w8\n\i[18]\i[26]\_q%(vera)\_q am i just jealous of what i don't have..?"
	}
	--
	" \n\n\f[color,default]\![*]\q[Ask something else,OnPeopleMenu]\n\![*]\q[I'm done,CANCEL]\e"
}

Select.abtlila : nonoverlap_pool
{
	posfeel += 0.5
	
	"\0\s[1]\_q%(vera)\_q oh, hey! questions for lila aren't done yet, cause phantasia's gotta sort the hollerfeed events first!! \w8\w8\n\s[3]\_q%(vera)\_q i'll talk more abt her later, yea?"
	
	--
	" \n\n\f[color,default]\![*]\q[Ask something else,OnPeopleMenu]\n\![*]\q[I'm done,CANCEL]\e"
}

Select.DreamMenu
{
	"\0(Placeholder for dream menu)\e"
	--
	"%(backtoq)"
}

Select.AboutYou
{
	"\0\_q%(vera)\_q my thoughts on %(pername)..? \w8\w8\n"
	-- //general friendship check
	if posfeel >= 10 && negfeel >= 10 
	{
		"\_q%(vera)\_q um, you're kinda hard to understand... \w8\w6\n"
		--
		if REPLACE(joinfeel,"-","") < 10 {
		"\_q%(vera)\_q like?? i can't decipher what you think about me at all..!"
		}
		elseif posfeel > negfeel {
		"\_q%(vera)\_q you're nice to me, but you also..."
		}
		elseif negfeel < posfeel {
		"\_q%(vera)\_q you're a jerk, but you also..."
		}
		--
		"\w8\w8\w6\n\_q%(vera)\_q ghhng \w6nevermind."
	}
	elseif posfeel >= 10
	{
		"\_q%(vera)\_q temp nice user dialogue! i like you!"
	}
	elseif negfeel >= 10
	{
		"\_q%(vera)\_q temp mean user dialogue! you smell!"
	}
	else //you've done almost nothing cmon dude step it up
	{
		"\s[2]\i[1]\i[80]\_q%(vera)\_q um, \w4you're... \w8\w8\n\s[1]\i[1]\i[80]\_q%(vera)\_q fine, \w8i guess? \w8\w5\n\s[2]\i[1]\i[80]\i[17]\_q%(vera)\_q it's not like you've done much to me or anything."
	}
	-- //add more later grgrgrgr
	"%(backtoq)"
}

Select.SnoopingOoh
{
	"\0(Placeholder for snooping menu)"
	--
	"%(backtoq)"
}

//created this mostly for 'about you', to abort it early if needed
backtoq {" \n\n\f[color,default]\![*]\q[Ask something else,OnQuestionMenu]\n\![*]\q[I'm done,CANCEL]\e"}

//----------Interact Menu----------

//huh for some reason i dont want the user to be able to give vera stuff. (she stays poor FOREVER you CANNOT help her)
OnInteractMenu : all
{
	"\0\b2\_q"
	"\f[align,center]\![set,autoscroll,disable]"
	"\n"
	
	"\![*]\q[CHECK SYLLADEX,OnSylladex]"
	"\n\f[height,-1]\f[color,gray]INVENTORY: Has unlocked games/activites.\f[color,default]\f[height,default]\n"
	
	"\n(DO)\n"
	if negfeel > 0; "\![*]\q[Apologize,apologizev]"
	else; "\f[color,gray]\![*]Apologize\f[color,default]"	
	//conditional. can pop up as soon as your negfeel is more than 0, but you can only apologize a few times per day + other parameters
	"\n"
	//"\![*]\q[Compliment,complimentv]  "
	"\![*]\q[Story,storyv]  "
	"\![*]\q[Comfort,comfortv]  "
	"\![*]\q[Joke,jokev]"
	"\n"
	"\![*]\q[Hug,hugv]  "
	"\![*]\q[Tease,teasev]  "
	"\![*]\q[Insult,insultv]"
	"\n"
	
	//games and such: move to sylladex? might be hard to find...
	"\n(GIVE)\n"
	"\![*]\q[A snack,snackv]  "
	"\![*]\q[A drink,drinkv]"
	"\n"
	"\![*]\q[Something hobby related,givehobbyv]\n"
	"\![*]\q[Something sleep related,givesleepv]\n"
	
	" \n\f[align,right]\![*]\q[Back,OnMenuRun]  \![*]\q[Cancel,CANCEL]\e"
}

Select.apologizev //ooohhhhhhrjg BIG apology function
{
	"\0\s[1]\i[1]\i[35]\i[80](You apologize to her.) \w8\w8\w8\n\n"
	--
	if negfeel <= 5 //you get unlimited apologies here bc you havent been TOO bad to her yk.
	{
		//fun fact for anyone reading my code (VENT JAIUWDHAIUED)
		//i was fresh off of a terrible argument when i started writing this function LMAO i um. im not projecting at all
		if Flag("just apologized") 
		{
			"\0\s[1]\i[1]\i[26]\i[80]\_q%(vera)\_q ...um. \w8\w8\n\s[2]\i[1]\i[26]\i[80]\_q%(vera)\_q i mean, it's not a big deal, but it seems kinda insincere when you JUST did this..."
			"\0\s[2]\i[1]\i[26]\i[35]\i[80](...She doesn't look offended, but it doesn't look like she's ready to forgive you yet.)"
		}
		else 
		{
			negfeel = 0
			AddFlag("just apologized")
			
			"\s[3]\i[1]\i[26]\i[80]\_q%(vera)\_q ...it's okay, \w2%(pername). \w8\w8\n"
			"\s[3]\i[1]\i[26]\i[80]\_q%(vera)\_q ...it's fine, \w2%(pername). \w8\w8\n"
			--
			if posfeel >= 25
			{
				"\s[3]\i[19]\i[26]\_q%(vera)\_q i didn't even think you were being mean at all? \w8\w8\n\s[3]\i[17]\_q%(vera)\_q i just thought sarcasm was your sense of humor yknow? \w8\w8\n\s[3]\i[26]\_q%(vera)\_q thanks, though."
				"\s[3]\i[19]\i[26]\_q%(vera)\_q i'm fine with you being a bit of a ""jerk"" since we're friends- it's funny! \w8\w8\n\s[3]\i[26]\_q%(vera)\_q but thanks for the apology."
			}
			else
			{
				"\s[2]\i[1]\i[80]\_q%(vera)\_q i mean, everyone makes mistakes..!"
				"\s[2]\i[1]\i[80]\_q%(vera)\_q you didn't do anything big, just try not to do it again, y'know?"
			}
		}
	}
	elseif negfeel < 15 //before the point of no return (lighthearted)
	{
		if (apologies > 5 && posfeel < 25) || (apologies > 10 && posfeel >= 25)
		{
			"\s[5]\i[1]\i[80]\![anim,clear,25]"
			--
			if posfeel < 25 {
			"(...That bridge's been burned.)"
			"(...She can't even look at you.)"
			}
			"(...Don't you think it's a bit late for that?)"
			"(...There's only so many times you can do this.)"
			"(...You know this won't work by now.)"
		}
		elseif Flag("just apologized")
		{
			"\0\s[2]\i[1]\i[26]\i[35]\i[80](...It doesn't look like she's ready to forgive you again yet.)"
		}
		else //i'd definitely like to expand this in the future bc theres barely any dialogue here
		{
			negfeel -= 5
			apologies++
			AddFlag("just apologized")
			
			if posfeel >= 25
			{
				//she's not being completely genuine (subconscious) but also i want to TRY and indicate that mixed feelings are okay?? like yeah you can have a teensy bit of negfeel if you like, if its less than 15 or so its not a bad thing?
				//its hard to put across in dialogue so i'll probably put this in her download page or whatever
				"\s[3]\i[26]\_q%(vera)\_q oh, um, it's fine, %(pername)! \w8\w8\n\_q%(vera)\_q we're friends! if you're a bit of a jerk, i don't mind all that much. \w8\w8\n\i[19]\_q%(vera)\_q ...thanks, though."
				"\s[3]\i[19]\i[26]\_q%(vera)\_q i-it's alright, %(pername)! \w8\w8\n\_q%(vera)\_q i mean, we're friends, so i can understand, y'know? \w8\w8\n\s[3]\i[26]\_q%(vera)\_q thanks, though."
			}
			elseif negfeel < 10
			{
				"\s[1]\i[1]\i[26]\i[80]\_q%(vera)\_q ...really? \w8\w8\n\s[4]\i[1]\i[26]\i[80]\_q%(vera)\_q "
				--
				"i forgive you, %(pername). \w6just don't be mean again, okay?"
				"it's okay, %(pername). \w6you won't do it again, right?"
			}
			else
			{
				"\s[1]\i[1]\i[18]\i[26]\i[35]\i[80](...She only looks at you and nods.) \w8\w8\n(It seems you're forgiven, sort of.)"
			}
		}
	}
	elseif negfeel < 50 //DEFINITELY need more dialogue for this but im not feeling it right now
	{
		if (apologies > 3 && posfeel < 50) || (apologies > 5 && posfeel >= 50)
		{
			"\s[5]\i[1]\i[80]\![anim,clear,25]"
			--
			if posfeel < negfeel {
			"(...That bridge's been burned.)"
			"(...She can't even look at you.)"
			}
			"(...Don't you think it's a bit late for that?)"
			"(...There's only so many times you can do this.)"
			"(...You know this won't work by now.)"
		}
		elseif Flag("just apologized")
		{
			"\0\s[2]\i[1]\i[26]\i[35]\i[80](...It doesn't look like she's ready to forgive you again yet.)"
		}
		elseif posfeel > negfeel
		{
			negfeel = 9
			posfeel += 5
			apologies++
			AddFlag("just apologized")
			
			"\s[1]\i[26]\_q%(vera)\_q ...you really mean it? \w8\w8\n\s[2]\i[17]\i[26]\_q%(vera)\_q ...i-i forgive you, \w6cause... \w8\w8\n\s[3]\i[26]\_q%(vera)\_q you'll be nicer from now on, right?"
		}
		else
		{
			negfeel = 14
			posfeel++
			apologies++
			AddFlag("just apologized")
			
			"\s[1]\i[1]\i[26]\i[35]\i[80](She looks at you in disbelief, but...) \w8\w8\n\s[2]\i[1]\i[26]\i[35]\i[80](...She nods, so it seems you're forgiven. \w8Sort of.)"
		}
	}
	else
	{
		"\s[5]\i[1]\i[80]\![anim,clear,25]"
		--
		"(An apology? \w6Now? \w8You must be kidding.)"
		"(As if she can hear you now.)"
		"(It's much too late to salvage this now.)"
		
		//Betrayal/high affection routes? better code this in the future sigh
		if posfeel > (negfeel / 2)
		{
		"(It'd be amusing to see her desperate for any sort of forgiveness from you. \w8Unfortunately, you think you're past that point by now.)"
		"(She would like that, wouldn't she? \w8Unfortunately, it seems a bit late for that.)"
		}
	}
	--
	" \n\n\f[color,default]\![*]\q[Go back,OnInteractMenu]\n\![*]\q[Close,CANCEL]\e"
}

//UGH i have no idea how to properly implement this function the way i want to. i am leaving it out. compliments are so hard.
Select.complimentv
{
	if negfeel >= 15
	{
		
	}
	elseif posfeel < 5
	{
		if negfeel <= 5
		{
			"\0\s[1]\i[1]\i[35]\i[80](You tell her that her shirt is cool.) \w8\w8\n\s[2]\i[1]\i[80]\_q%(vera)\_q um... \w8\w8\n\s[1]\i[1]\i[80]\_q%(vera)\_q thanks..?"
		}
		else
		{
			
		}
	}
	else 
	{
		
	}
	--
	" \n\n\f[color,default]\![*]\q[Go back,OnInteractMenu]\n\![*]\q[I'm done,CANCEL]\e"
}

Select.storyv
{
	if negfeel >= 15
	{
		
	}
	elseif posfeel < 5
	{
		if negfeel <= 5
		{
			posfeel += 0.1
			
		}
		else
		{
			
		}
	}
	else 
	{
		
	}
	--
	" \n\n\f[color,default]\![*]\q[Go back,OnInteractMenu]\n\![*]\q[I'm done,CANCEL]\e"
}

Select.comfortv
{
	"(Placeholder comforting!)"
	--
	" \n\n\f[color,default]\![*]\q[Go back,OnInteractMenu]\n\![*]\q[I'm done,CANCEL]\e"
}


Select.jokev
{
	"(Placeholder jokes!)"
	--
	" \n\n\f[color,default]\![*]\q[Go back,OnInteractMenu]\n\![*]\q[I'm done,CANCEL]\e"
}


Select.hugv
{
	"(Placeholder hugs!)"
	--
	" \n\n\f[color,default]\![*]\q[Go back,OnInteractMenu]\n\![*]\q[I'm done,CANCEL]\e"
}

Select.teasev
{
	"(Placeholder teasing!)"
	--
	" \n\n\f[color,default]\![*]\q[Go back,OnInteractMenu]\n\![*]\q[I'm done,CANCEL]\e"
}

Select.insultv
{
	"(Placeholder insulting!)"
	--
	" \n\n\f[color,default]\![*]\q[Go back,OnInteractMenu]\n\![*]\q[I'm done,CANCEL]\e"
}

OnSylladex : all
{
	"\_q"
	"\![*]\q[Make Origami,OnOrigamiMenu]\n" 	//} else {"\![*]---- -------\n"}
	"\![*]\q[Tarot Cards,OnTarotQ]\n" 			//} else {"\![*]----- -----\n"}
	"\![*]\q[Candle Making,OnCandleMenu]\n" 	//} else {"\![*]------ ------\n"}
	"\![*]\q[Dream Journal,OnJournalMenu]\n" 	//} else {"\![*]----- -------\n"}
	
	" \n\f[align,right]\![*]\q[Back,OnInteractMenu]  \![*]\q[Cancel,CANCEL]\e"
}

OnOrigamiMenu
{
	
}

OnTarotQ
{
	//"hm, ok, i can only do general daily/3 card readings, so..."
	//idk abt eventually adding the option to ask her a question before the reading but ehhhh
}

OnCandleMenu
{
	//like cooking in hunter smoker!!
	//chose base + scents + stuff like that
	//candle counter, candle menu can be either to use one or to make one :D
	//takes a day for the candles to be made but you can speed up the process thru options lol
	//if you close vera when you have more than 5 candles, she'll use one at home (she tells you this in candle faq/once you have 5 current candles) thus reducting it from the candle count (she uses the oldest candle youve made)
}

//----------Statuses Menu----------

OnStatMenu : all
{
	_d = debug
	
	"\0\_q\b2\![set,autoscroll,disable]"
	
	"\f[align,center]—STATS—\n"
	if _d 	{"\![*]\q[Quick reset,quickreset] \n"
			"\f[height,-2]\f[color,gray](no i wont explain setting variables, WYSIWYG. please don't break anything by setting negatives...) \n\f[height,default]\f[color,default]\n"}
	else 	{"\n\n"}
	
	"\f[align,default]"
	
	if reference0 != "" {"\![open,inputbox,OnSetStat,-1,--reference=%(reference0)]"}
	// whats annoying abt this ^ is that detecting if reference != "" is kinda finnicky...
	
	"\![*]"
	if _d 	{"\q[Total Pets:,OnStatMenu,pet] "}
	else 	{"Total Pets: "}
			"%(totaltouch[0])\n"
	" ("
	if _d 	{"\q[Pet Highscore:,OnStatMenu,phs] "}
	else 	{"Pet Highscore: "}
			"%(pethighscore[1]))\n"
	"\![*]"
	if _d 	{"\q[Total Boops:,OnStatMenu,boop] "}
	else 	{"Total Boops: "}
			"%(totaltouch[2])\n" 		//change to punches on negative route?
	"\![*]"
	if _d 	{"\q[Total Hair Brushes:,OnStatMenu,brush] "}
	else 	{"Total Hair Brushes: "}
			"%(totaltouch[1])\n"
	
	"\n\![*]"
	if _d 	{"\q[Positve feelings:,OnStatMenu,pos] "}
	else 	{"Positve feelings: "}
			"%(PrintStat(posfeel))\n"
	"\![*]"
	if _d 	{"\q[Negative feelings:,OnStatMenu,neg] "}
	else 	{"Negative feelings: "}
			"%(PrintStat(negfeel))\n "
	
	"\n\n\f[align,center]\_a[OnReset][RESET GAME]\_a "
	"\n\n\f[align,right]\q[Back to the menu,OnMenuRun] \![*]\e"
}

PrintStat //this is the worst possible way to get rid of the trailing 0s omg (i don't even mind the like, 1.0 i want to show that it's possible to get a decimal in Feelies)
{
	//you could do a if gettype == 2 here to use for other cases but idk not relevant yet
	_str = TOSTR(_argv[0])
	_out = REPLACE(_str,"00000","")
	_out
}

Select.quickreset
{
	"\0\_q(You sure? This is a debug function that resets all the listed and related stats without starting the firstboot sequence.) "
	--
	"\n\n\![*]\q[ye,yesqr]  \![*]\q[no!!!,OnStatMenu,]\e"
}
Select.yesqr
{
	pethighscore[0] = 0	 //The petting high score, as the number of the stroke value
	pethighscore[1] = 0	 //The petting high score, as the number of pets
	totaltouch[0] = 0	 //The total times the ghost has been pet
	totaltouch[1] = 0 	 //total brushes
	totaltouch[2] = 0 	 //total boops
	posfeel = 0
	negfeel = 0
	if allrecenttouch > 0; {recenttouch = (0,0,0)}
	--
	"\![raise,OnStatMenu,]"
}
OnSetStat 	
{
	if reference2 == "pet"
	{
		totaltouch[0] = TOINT(reference0)
	}
	if reference2 == "phs"
	{
		pethighscore[1] = TOINT(reference0)
		_p = TOINT(reference0) * 40
		pethighscore[0] = _p
	}
	if reference2 == "boop"
	{
		totaltouch[2] = TOINT(reference0)
	}
	if reference2 == "brush"
	{
		totaltouch[1] = TOINT(reference0)
	}
	if reference2 == "pos"
	{
		posfeel = TOREAL(reference0)
	}
	if reference2 == "neg"
	{
		negfeel = TOREAL(reference0)
	}
	--
	"\![raise,OnStatMenu,]"
}

OnReset
{
	"\0\_q(Are you sure? Your game progress cannot be restored. The same may not be said for your memories.) "
	--
	"\n\n\![*]\q[YES,OnResetConfirm]  \![*]\q[NO,OnStatMenu,]\e"
}

//OnResetConfirm is in ae_bootend.dic

//----------Function Menu----------
OnFunctionMenu : all
{
	"\0\b2"
	
	//This will make the opening dialogue random (ignoring the all modifier) with a nonoverlap modifier, if you add more dialogues
	nonoverlap:{ 
	"\s[1]\_q%(vera)\_q huh..? \w4yeah, \w1i can do things..!"
	"\s[1]\i[1]\i[80]\_q%(vera)\_q o-oh, \w2um, \w2yes..!"
	}
	"\n\n\f[color,default]\_q"
	
	"\![*]\q[Check for SSP updates,script:\![update,platform\]]\n"
	"\![*]\q[Check for Vera updates,UpdateCheck]\n\n"
	
	"\![*]\q[Check Email,script:\![biff\]]\n"
	"\![*]\q[Fix the Clock,script:\![execute,sntp\]]\n"
	"\![*]\q[Open Calendar,OpenCalendar]\n"
	"\![*]\q[Open SSP Preferences,SSPPrefs]\n"
	"\![*]\q[Empty Recycle Bin,RecycleBin]\n"
	"\![*]\q[Show System Information,SystemInfo]\n"
	"\![*]\q[Report a bug,BugReport]\n"
	
	"\n"
	"\f[align,right]\q[Back to main menu,OnMenuRun] \![*]\e"
}

Select.OpenCalendar
{
	"\0\s[1]\i[41]\i[26]\_q%(vera)\_q um... is this it..?"
	--
	"\![open,calendar]\e"
}

Select.SSPPrefs
{
	"\0\s[3]\i[26]\_q%(vera)\_q um, i think this looks right..."
	--
	"\![open,configurationdialog]\e"
}

Select.RecycleBin
{
	_gomicheck = FUNCTIONEX("saori/gomi.dll", "-n") //Gets the number of items in the recycle bin
	
	"\s[1]\i[1]\i[80]\_q%(vera)\_q oh, ok... \w8\i[18]i'm on it... \w8\w8\s[-1]\w8\w8\w8\w8\n\n"
	--
	if _gomicheck > 0 //If there was something to empty
	{
		_gomi = FUNCTIONEX("saori/gomi.dll", "-f") //Calling the Saori to empty the recycle bin
		_s = ""; if _gomicheck != 1; _s = "s" //Plural check
		
		"\s[1]\i[1]\i[80]\n\_q%(vera)\_q it's done... \w8\w8\n\_q%(vera)\_q i think i cleared out %(_gomicheck) thing%(_s), if you wanted to keep track or something..."
	}
	else //If it's empty
	{
		"\s[2]\i[1]\i[80]\i[26]\w2\n\_q%(vera)\_q ...i-i'm sorry, \w2um... \w6i couldn't \w4find any trash, \w4so... \w6yeah..."
	}
	--
	"\n\n\f[color,default]\![*]\q[Back to functions menu,OnFunctionMenu]\n\![*]\q[I'm done,CANCEL]\e"
}

Select.SystemInfo : all //Partially copied from the GT template, but I tweaked a few things
{
	os_name = FUNCTIONEX("saori/saori_cpuid.dll","os.name")
	os_version = FUNCTIONEX("saori/saori_cpuid.dll","os.version")
	cpu_name = FUNCTIONEX("saori/saori_cpuid.dll","cpu.name")
	cpu_clockex = FUNCTIONEX("saori/saori_cpuid.dll","cpu.clockex")
	
	"\0\b[2]\_q\f[anchorstyle,none]"
	
	"OS: \_a[OnBlank]%(os_name) %(os_version)\_a\n"
	"Memory Load: \_a[OnBlank]%(memoryload)%\_a\n"
	"Physical Memory: \_a[OnBlank]%(memoryavailphys) KB\_a\n"
	"CPU: \_a[OnBlank]%(cpu_name) %(cpu_clockex)MHz\_a\n"
	"Uptime: \_a[OnBlank]%(systemuphour):%(systemupminute):%(systemupsecond)\_a\n\n"
	
	"Ukagaka: \_a[OnBlank]%property[baseware.version]\_a\n\n"
	
	"\![*]\q[Back to functions,OnFunctionMenu]\n"
	"\![*]\q[Done,CANCEL]\e"
}

Select.BugReport
{
	"\0\s[3]\i[26]\_q%(vera)\_q there's bugs here..?? \w8\i[19]i think you should tell \_a[https://whataphantasia.carrd.co]'phantasia'\_a about%(br)that... \w8\n\s[3]\i[41]\i[26]\_q%(vera)\_q um, \w4at least, \w6that's what this bug exterminator card says. \w8found it when i got here!"
	--
	"\n\n\f[color,default]\![*]\q[Back to functions,OnFunctionMenu]\n\![*]\q[I'm done,CANCEL]\e"
}

Select.UpdateCheck //Starts an update check
{
	"\![updatebymyself]\e"
}



//----------Config Menu----------
OnConfigMenu : all //stuff like name/pronouns/birthday is in user.dic :D
{
	FixDefaultOptions
	
	if "OnUserBorn" _in_ reference0; {birthday = oldbirthday; ERASEVAR("oldbirthday")} //resets the birthday to the old values, erases the temp variable
	
	"\0\_q\b2\![close,inputbox,%(reference0)]\![set,autoscroll,disable]"
	
	"\f[align,center]—CONFIG—\n\n\f[align,default]"
	
	"Your name: \![*]\_a[OnChangeTitle]%(menuprefix)\_a \![*]\_a[OnChangeName]%(username)\_a\n"
	"Hollercode: \![*]\_a[OnChangeCode]placeholder\_a\n" //TODO: implement asking for hollercode
	"Your pronouns: \![*]\_a[OnChangePronoun]%(Capitalize(they))/%(Capitalize(them))\_a\n"
	"Your birthday: \![*]\_a[OnChangeBirthday]%(birthday[0])\_a\n"
	"Your hemisphere: \![*]\_a[OnToggleHemisphere]%(hemisphere)\_a\n\n"
	
	"\![*]Talk rate: \_a[OnChangeTalk]%(Capitalize(talkrate))\_a\n"
	"\![*]Readability: \_a[OnToggleRead]%(readability)\_a\n"
	"\![*]Hour Notifications: \_a[OnToggleHourNotifs]%(HourNotifs)\_a\n"
	"\![*]\_a[OnContentMenu]Sensitive Content Configuration\_a\n\n"
	
	"\![*]Auto update checks: \_a[OnToggleUpdate]%(AutoUpdates[0])\_a \n"
	//"\![*]Enable Uninstall? \_a[OnToggleUninstall]%(deleteshitai)\_a\n" //removing for now. i dont find a use for this button other than as a failsafe..!!
	
	"\n\f[align,right]\q[Back to the menu,OnMenuRun] \![*]\e"
}

//This is here in case the variables get erased. The buttons will be set to default so the user has something to click in the config menu. This has happened to my users before and resetting them is a painful process. Do not remove these, you'll want to add in new ones for any toggle buttons you have in the config menu. This is also called in BootVars and UpdateVars, which is why it's separate from the config menu itself.
FixDefaultOptions
{
	if username == ""; username = "stranger"
	if birthday[0] == ""; birthday[0] = "???"
	if hemisphere == ""; hemisphere = "Northern"
	if talkrate == ""; {talkrate = "every 5 minutes"; aitalkinterval = 300}
	if deleteshitai == ""; deleteshitai = "On"
	if pronouns == ""; pronouns = "undef"
	if (pronounplur != "plur") || (pronounplur != "sing") {
		if pronouns == "undef"; pronounplur = "plur"
		else; pronounplur = "sing"
	}
	if readability == ""; readability = "Full Color"
	if AutoUpdates[0] == ""; AutoUpdates[0] = "On"
	if HourNotifs == ""; HourNotifs = "On"
}

OnToggleRead
{
	if readability == "Full Color"; readability = "Limited Color"
	elseif readability == "Limited Color"; readability = "No Color"
	else; readability = "Full Color"
	--
	OnConfigMenu
}

OnToggleHemisphere
{
	if hemisphere != "Northern"; hemisphere = "Northern"
	else; hemisphere = "Southern"
	--
	OnConfigMenu
}

//sensitive content menu
//---remember to put a message in updates if u ever add or remove a topic!
OnContentMenu : all
{
	"\0\_q\![set,autoscroll,disable]\f[align,center]\n"
	
	if AcceptTopics[0] == 1
	{"\![*]\q[Death,OnContentToggle,0]"}
	else
	{"\![*]\f[strike,1]\q[Death,OnContentToggle,0]\f[strike,0]"}
	"\n"
	if AcceptTopics[1] == 1
	{"\![*]\q[Suicidal Ideation,OnContentToggle,1]"}
	else
	{"\![*]\f[strike,1]\q[Suicidal Ideation,OnContentToggle,1]\f[strike,0]"}
	"\n"
	if AcceptTopics[2] == 1
	{"\![*]\q[Derealization,OnContentToggle,2]"}
	else
	{"\![*]\f[strike,1]\q[Derealization,OnContentToggle,2]\f[strike,0]"}
	"\n"
	if AcceptTopics[3] == 1
	{"\![*]\q[LGBTphobia,OnContentToggle,3]"}
	else
	{"\![*]\f[strike,1]\q[LGBTphobia,OnContentToggle,3]\f[strike,0]"}
	"\n"
	if AcceptTopics[4] == 1
	{"\![*]\q[Ableism,OnContentToggle,4]"}
	else
	{"\![*]\f[strike,1]\q[Ableism,OnContentToggle,4]\f[strike,0]"}
	
	"\n\n[half](Topics around neglect will always be present.)"
	
	"\n\n\f[align,right]\q[Back to Config,OnConfigMenu] \![*]\e"
}

OnContentToggle
{
	//0 - death
	//1 - suicide
	//2 - derealization
	//3 - lgbtphobia
	//4 - ableism
	_topic = TOINT(reference0)
	
	if AcceptTopics[_topic] == 1 	{AcceptTopics[_topic] = 0}
	else 							{AcceptTopics[_topic] = 1}
	--
	OnContentMenu
}

OnChangeTalk : all
{
	nonoverlap:{
	"\0\s[1]\_q%(vera)\_q oh, how often should i talk..? \w8\s[2]\i[41]\n\_q%(vera)\_q um, right now it's %(talkrate)...\n\n"
	}
	
	"\f[color,default]"
	"\![*]\q[Every 30 seconds,OnTalkRate,30]\n"
	"\![*]\q[Every minute,OnTalkRate,60]\n"
	"\![*]\q[Every 3 minutes,OnTalkRate,180]\n"
	"\![*]\q[Every 5 minutes,OnTalkRate,300]\n"
	"\![*]\q[Don't speak,OnTalkRate,0]\n\n"
	
	"\f[align,right]\q[Nevermind,OnConfigMenu] \![*]\e"
}

OnToggleHourNotifs
{
	if HourNotifs != "On"; HourNotifs = "On"
	else; HourNotifs = "Off"
	--
	OnConfigMenu
}

OnTalkRate
{
	aitalkinterval = TOINT(reference0) //this changes the talk rate, in seconds. reference0 is sent from the menu options above
	
	if reference0 == "30"
	{
		talkrate = "every 30 seconds" //this displays the talk rate in the menu
		
		"\0\s[3]\i[1]\i[80]\i[26]\_q%(vera)\_q t-that often..? \w8\n\i[19]\_q%(vera)\_q i'll try!!"
	}
	elseif reference0 == "60"
	{
		talkrate = "every minute"
		
		"\0\s[1]\i[1]\i[80]\_q%(vera)\_q oh... \w8\s[3]\i[1]\i[80]\i[19]\i[26]i'll try!"
	}
	elseif reference0 == "180"
	{
		talkrate = "every 3 minutes"
		
		"\0\s[1]\i[1]\i[80]\_q%(vera)\_q ok, i can do that!"
	}
	elseif reference0 == "300"
	{
		talkrate = "every 5 minutes"
		
		"\0\s[1]\i[1]\i[80]\_q%(vera)\_q oh, \w6ok. \w8\w8\i[18]\n\_q%(vera)\_q hmm... \w6lots of time to think..."
	}
	else
	{
		talkrate = "never"
		
		"\0\s[1]\i[1]\i[80]\i[18]\_q%(vera)\_q ok, got it..."
	}
	--
	"\n\n\f[color,default]\![*]\q[Back to config,OnConfigMenu]\n\![*]\q[I'm done,CANCEL]\e"
}

OnToggleUpdate
{
	if AutoUpdates[0] != "On"; AutoUpdates[0] = "On"
	else; AutoUpdates[0] = "Off"
	--
	OnConfigMenu
}

OnToggleUninstall
{
	if deleteshitai != "Off"; deleteshitai = "Off"
	else; deleteshitai = "On"
	--
	OnConfigMenu
}

//----------Misc Functions----------

//Menu has timed out
OnChoiceTimeout
{
	"\0\s[2]\i[18]\i[1]\i[80]\i[35](It looks like she's lost focus...)\e"
}

//tbf this doesnt even need to be a thing but having it there feels nice
Select.CANCEL
{
}

	//_num = ANY(1,2,3)
	//"i wonder what %(PeopleArray[(_num)]) is doing right now...\e"
	//%(ANY('skul,ulys,lila'))

OnTestVar
{
	"\0\s[1]\_q%(vera)\_q anchor test! @skul, @ulys, @lila, @dad!!\e"
	//LOGGING("This is a debug function! This text will show up in Tama, or in the SHIORI log! You can also output variables and such with this function. This is super handy for debugging without having to output data to your ghost's balloon! Especially for things like loops, where you need to see certain variables at every step of the loop. Make good use of it!")
}


//============Kero Menu============

OpenKeroMenu
{
	"\1\s[10]Uh... You shouldn't be able to see this haha"
	--
	OnKeroMenu
}

OnKeroMenu : all
{
	//Fill in as you like
}