OnTranslate
{
	_talk = reference0
	
	//---Auto Anchors---
	//Add as many names as you want to this list
	_namelist = "skul,ulys,lila,luce,dad,mom"
	//HollercodeArray = {"instantiator","prospector","ruminator","revisor"} in word.dic
	
	//Names written here should be written in dialogue as @Name 1, @Name 2, etc... So if I added Lulo, I would write @Lulo in dialogue to call the anchor. If I wanted to write the name Lulo without it being an anchor, I would just write it as Lulo
	//When these anchors are clicked, they will direct to an Anchor.function. So for Name 1 it would be Anchor.Name_1 and for Lulo it would be Anchor.Lulo
	//Invalid characters will be converted to underscores. You can see a list of invalid characters below. Names are case sensitive as well!

	//Loops through every name on the list and checks for them in dialogue
	foreach _namelist; _name
	{
		//List of characters that are invalid for function names
		_invalidcharacters = (" ",'"',"#","$","%","&","(",")","*","+","-","/",":",";","<","=",">","?","@","[","]","`","{","|","}",",")
		_funcname = _name
		foreach _invalidcharacters; _char //Loop through each invalid character and replace them with _ if they exist
		{
			if _char _in_ _funcname; _funcname = REPLACE(_funcname,_char,"_")
		}
		_talk = REPLACE(_talk,"@%(_name)","\_a[AUTO_ANCHOR:%(_funcname)]%(_name)\_a") //Replace the @name in the script with an anchor. Since the auto anchor tag uses a :, and : is already cleaned out of the anchor name, you should never be able to interfere with that tag.
	}
	
	
	//obsidian stuff
	//	- later define for standing and awakened forms
	//	- vera shouldnt be able to go into her idles 12-14 when holding obsidian
	//	- 210 is coded into 6-9 surfaces but it might cause unwanted shell/text behavior.
	//	- vera's hollercode talks are unavailble while holding obsidian.
	
	//sit surfaces whose arms are cancelled out by \s[210]
	_verasitsurfaces = "\s[0],\s[1],\s[2],\s[3],\s[4],\s[5],\s[11]"
	//sit surfaces that ARENT cancelled out
	_verasitidle = "\s[6],\s[7],\s[8],\s[9],\s[12],\s[13],\s[14]"
	
	if holdingObsidian
	{
		foreach _verasitsurfaces; _s
		{
			_talk = REPLACE(_talk, "%(_s)", "%(_s)\i[210]")
		}
		foreach _verasitidle; _ss
		{
			//hopefully this works lol
			_talk = REPLACE(_talk, "%(_ss)", "\![get,property,OnObsidianJumpDown,currentghost.scope(0).rect]\e")
		}
	}
	//(this is digi's code for replacing all surface tags automatically!! reason for commenting out below the replace code
	/*
	if holdingObsidian
	{
		_suregex = RE_GREP(_talk, '(?<=\\s\[).+?(?=\])')
		_surnum = RE_GETSTR()
		foreach _surnum; _surface
		{
			LOGGING("ForEachLoop" + _surface)
			_talk = REPLACE(_talk, "\s[%(_surface)]", "\s[%(_surface)]\i[210]")
			//ehh i hope this doesnt do anything weird with keros... i only use it once in vera but im planning to use it MORE soon so it might be an issue...
		}
	}
	*/
	
	
	//---Manual Anchors---
	//You can use these in conjunction with the auto anchors! Auto anchors are good for setting up simple links to names and such, but if you need something more complex you can still easily put it here and it should not interfere. Don't forget that if you need a one-off anchor, you can just write them directly in dialogue as well.
	_talk = REPLACE(_talk,"Example Anchor","\_a[exampleanchor]Example Anchor\_a")
	_talk = REPLACE(_talk,"Example Anchor with OnFunction","\_a[OnSomeAnchor]Since this links to OnSomeAnchor, you could create a function called OnSomeAnchor that would be called directly by clicking this\_a")
	_talk = REPLACE(_talk,"Example word swap","You don't have to just make anchors, you could also use this to insert images and such")
	
	
	
	//---Important other stuff, written by Ponapalt---
	if reference1 == "" && reference2 == ""
	{ //send input box : no event (ref2) , no special flag (ref1)
		EVAL('"' + REPLACE(_talk,'"','" + CHR(0x22) + "') + '"')
	}
    else
	{ //event result from ghost
		TOSTR(_talk)
	}
}

//This bit of code is what makes menu options that don't start with On direct to Select.options instead. Remove or comment it if you prefer!
OnChoiceSelect
{
	EVAL("Select.%(reference0)")
}

OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0 //checking to see if the link is a web address
	{
		"\j[%(reference0)]\e"
	}
	elseif "AUTO_ANCHOR:" _in_ reference0 //Checks for the Auto Anchor tag. If it exists, runs a function starting with 'Anchor.' The name of the function, as set up above, will be appended to the end. Example: Anchor.Name_1 or Anchor.Lulo
	{
		EVAL("Anchor.%(SPLIT(reference0,'AUTO_ANCHOR:')[1])")
	}
	else
	{
		case reference0
		{
			//---HOMESTUCK TERMINOLOGY: for those who havent read homestuck and have no idea what this stuff means
			when "fetchmodus"
			{
				"\0\b2\s[3]\i[26]\_q%(vera)\_q ah, \w2yeah, \w2i'm not good with fetch modi either. \w8\w8\n\s[2]\i[41]\_q%(vera)\_q they're ""data structures,"" \w6\i[17]but i don't know what that means- \w8\s[1]\i[41]just think of them as the process of how you get stuff out of your \_a[sylladex]sylladex.\_a \w8\w8\n\s[3]\i[41]\_q%(vera)\_q like my origami modus, which makes you fold an approximation of what you want from your \_a[sylladex]sylladex!\_a"
				--
				"\n\n\f[align,center]\_b[img\origamimodus.png,inline,--option=use_self_alpha]\x\e"
			}
			when "sylladex"
			{
				"\0\b2\s[3]\i[26]\_q%(vera)\_q a sylladex?? \w8\w8\n\i[19]\_q%(vera)\_q everyone has one. \w8\w8\n\s[2]\_q%(vera)\_q it's where you store all your stuff on hand, \w2and you get stuff out of it through your \_a[fetchmodus]fetch modus.\_a \w8\w8\n\s[1]\_q%(vera)\_q simple stuff! \w8\s[3]\i[19]here's one of my empty sylladex cards :D"
				--
				"\n\n\f[align,center]\_b[img\sylladexcardempty.png,inline,--option=use_self_alpha]\x\e"
			}
			when "strifespecibus"
			{
				"\0\b2\s[3]\i[26]\_q%(vera)\_q oh, the strife specibus is pretty convoluted for those not used to it. \w8\w8\n\s[1]\i[41]\_q%(vera)\_q it contains everything you need to \_a[strife]strife,\_a like your kind abstrati and strife deck and all that. \w8\w8\n\s[2]\i[41]\_q%(vera)\_q hmm... \w6\s[1]\i[41]ok, the kind abstratus determines what kind of weapons you can use, \w6and the strife deck contains all of the things you've assigned under that kind abstratus. \w8\w8\n\s[3]\_q%(vera)\_q take for example my pillowkind abstratus! \w8i can allocate pillows and plushies and stuff to it, so i can proficiently use them in a \_a[strife]strife\_a :D \w8\w8\n\i[19]\_q%(vera)\_q make sense?\x\e"
			}
			when "strife"
			{
				"\0\b2\s[1]\i[1]\i[43]\i[61]\_q%(vera)\_q ...a strife? \w8\w8\n\_q%(vera)\_q have you never been in one? \w8\w8\n\s[2]\_q%(vera)\_q it's like, \w4when someone engages you in... \w2a battle, \w2or a fight. \w8\w8\n\s[1]\_q%(vera)\_q conflicts happen all the time and you just gotta strife it out. \w8\w8\n\s[2]\i[26]\i[41]\_q%(vera)\_q i don't like it when they get more violent, but everyday strifes are more like puzzles..! \w8\w8\n\s[3]\i[26]\_q%(vera)\_q i dunno %(pername), i've never had to think about this before. \w8\w8\n\_q%(vera)\_q it's just a process for disagreements and stuff. \w6you don't fight without initiating a strife first. \w4\i[19]dunno why you wouldn't!"
				--
				"\n\n\f[align,right]\_b[img\abscond(s).png,inline,--option=use_self_alpha]\x\e"
			}
			//-------------------
			when "violentghost"
			{
				"\0\s[1]\_q%(vera)\_q oh, \w6the violentghost forums are pretty obscure... \w8\w8\n\s[2]\i[41]\_q%(vera)\_q it's just focused on talking about ghost stories and paranormal encounters. \w8i mostly check it because the stories are interesting, not that i completely believe whats on there. \w8\w8\n\s[1]\_q%(vera)\_q the userbase is small and nice, and i met by friends on there, so... \w8\w8\n\s[3]\i[19]\_q%(vera)\_q it's cool!\e"
			}
		}
	}
}


Anchor.dad
{
	if posfeel < 50
	{	
		"\0\s[2]\i[1]\i[80]\_q%(vera)\_q oh, \w6um, \w6my dad, \w6he... \w8\w8\n\i[18]\_q%(vera)\_q he takes care of me, \w6he pays for our apartment... \w8\w8\n\s[3]\i[1]\i[80]\_q%(vera)\_q he's really funny and hardworking!\w8\w2\n\i[18]\i[26]"
		--
		if KnowAbout[0] < 1 	{"\_q%(vera)\_q i just wish he... \w8\w8\n\s[5]\i[1]\i[80]\![anim,stop,25]\_q%(vera)\_q ...y'know."}
		else 					{"\_q%(vera)\_q that's really all i have to say about him..."}
	} 
	else //if KnowAbout[0] == 1 (set this up bc im planning on using more than just 0 and 1)
	{
		"\0\s[1]\i[1]\i[80]\i[18]\_q%(vera)\_q my dad... \w8\w8\n\s[2]\i[17]\i[1]\i[80]\_q%(vera)\_q he's... \w6y'know. \w8\w8\n\_q%(vera)\_q he's trying his best to provide for me and himself, \w6especially since it's just us, \w6so... \w8\w8\n\s[5]\i[1]\i[80]\![anim,stop,25]\_q%(vera)\_q yeah. \w6no biggie."
	}
	--
	if KnowAbout[0] == 0 {KnowAbout[0] = 1}
	
	"\n\n\f[color,default]\![*]\q[Go to question menu,OnPeopleMenu] \n\![*]\q[Close,CANCEL]\e"
}
Anchor.luce
{
	"\0\s[1]\i[1]\i[80]\_q%(vera)\_q ah, \w6um, \w6that's just my dad's name... \w8\w8\n"
	--
	if posfeel < 50
	{
		"\s[2]\i[1]\i[80]\i[18]\_q%(vera)\_q he takes care of me, \w6he pays for our apartment... \w8\w8\n\s[3]\i[1]\i[80]\_q%(vera)\_q he's really funny and hardworking!\w8\w2\n\i[18]\i[26]"
		--
		if KnowAbout[0] < 1 	{"\_q%(vera)\_q i just wish he... \w8\w8\n\s[5]\i[1]\i[80]\![anim,stop,25]\_q%(vera)\_q ...y'know."}
		else 					{"\_q%(vera)\_q that's really all i have to say about him..."}
	} 
	else
	{
		"\s[2]\i[17]\i[1]\i[80]\_q%(vera)\_q he's... \w6y'know. \w8\w8\n\_q%(vera)\_q he's trying his best to provide for me and himself, \w6especially since it's just us, \w6so... \w8\w8\n\s[5]\i[1]\i[80]\![anim,stop,25]\_q%(vera)\_q yeah. \w6no biggie."
	}
	--
	if KnowAbout[0] == 0 {KnowAbout[0] = 1}
	
	"\n\n\f[color,default]\![*]\q[Go to question menu,OnPeopleMenu] \n\![*]\q[Close,CANCEL]\e"
}
Anchor.mom
{
	if KnowAbout[1] == 0 {KnowAbout[1] = 1}
	
	if posfeel < 50
	{
		"\s[1]\i[1]\i[80]\_q%(vera)\_q my... \w6mom..? \w8\w8\n\s[2]\i[1]\i[80]\_q%(vera)\_q uh, \w2i don't really know that much about her, \w6but my dad really likes her... \w8\w8\n\s[3]\i[19]\i[1]\i[80]\i[26]\_q%(vera)\_q so i like her too..!"
	} 
	else
	{
		"\s[4]\i[17]\i[26]\_q%(vera)\_q ...i wish i could tell you about her, \w8but i never really met her. \w8\w8\n\![anim,stop,17]\i[19]\_q%(vera)\_q @dad won't even tell me all that much about her, \w6so i don't know if she died or left or whatever. \w8\w8\n\s[5]\![anim,stop,25]\i[1]\i[80]\_q%(vera)\_q but from the way @dad talks about her, \w2she always wanted me to be happy and stuff..."
	}
	--
	"\n\n\f[color,default]\![*]\q[Go to question menu,OnPeopleMenu] \n\![*]\q[Close,CANCEL]\e"
}
Anchor.skul
{
	if KnowAbout[2] == 0 {KnowAbout[2] = 1}
	
	"\0\s[1]\i[1]\i[80]\i[1030]\_q%(vera)\_q o-oh, \w6skul, \w6he... \w8\w8\n\s[3]\i[1]\i[80]\i[19]\i[26]\i[1030]\_q%(vera)\_q um, \w4i met him a few years ago on this one para-science forum. \w8\w8\n\s[4]\i[1]\i[41]\i[61]\i[26]\_q%(vera)\_q he was kinda the first person i really befriended online... \w8\w8\n\i[19]\_q%(vera)\_q um, \w4all you need to know about him is that he's like, \w4one of those conspiracy theory guys- \w4he's really secretive because he hates that the government is watching him and stuff. \w8\w8\n\s[3]\i[1]\i[80]\i[26]\_q%(vera)\_q he's really yelly, \w4but he looks out for us... \:)"
	--
	"\n\n\f[color,default]\![*]\q[Go to question menu,OnPeopleMenu] \n\![*]\q[Close,CANCEL]\e"
}
Anchor.ulys
{
	if KnowAbout[3] == 0 {KnowAbout[3] = 1}
	
	"\0\s[1]\i[1]\i[80]\_q%(vera)\_q oh, \w4ulys..! \w8\w8\n\s[2]\i[1]\i[80]\_q%(vera)\_q they're probably the most adjusted one out of all of us... \w8\w8\n\s[3]\i[1]\i[80]\i[18]\_q%(vera)\_q i met them on that one para-science forum- \w2\s[3]\i[1]\i[80]they're very particular about mythological creature lore... \w8\w8\n\s[2]\i[1]\i[41]\_q%(vera)\_q ...oh, \w4i'm rambling at this point, \w4but \w4ulys is basically... \w8\w8\n\s[3]\i[1]\i[80]\i[28]\_q%(vera)\_q the responsible-but-takes-no-shit older sibling of the friend group..! \w8\w8\n\s[3]\i[19]\i[1]\i[80]\i[26]\_q%(vera)\_q i-if that... makes sense."
	--
	"\n\n\f[color,default]\![*]\q[Go to question menu,OnPeopleMenu] \n\![*]\q[Close,CANCEL]\e"
}
Anchor.lila
{
	if KnowAbout[4] == 0 {KnowAbout[4] = 1}
	
	"\0\s[2]\i[1]\i[41]\_q%(vera)\_q ah, \w4lila is... \w8\w8\n\i[18]\_q%(vera)\_q i met her through "
	--
	if KnowAbout[3] >= 1 {"ulys,"} else {"her partner,"}
	--
	" \w6unlike the rest of my friends. \w8\w8\n\s[3]\i[1]\_q%(vera)\_q she has an eye for fashion and makeup... \w8\n\i[19]\_q%(vera)\_q and she's always trying to keep everyone in good spirits \^v\^"
	--
	if KnowAbout[2] >= 1 {" \w8\w8\w8\n\s[1]\i[17]\i[27]\i[1]\i[80]\i[1030]\_q%(vera)\_q ...i just wish a certain CONSPIRACY THEORIST would stop being so stand-offish to her."}
	--
	"\n\n\f[color,default]\![*]\q[Go to question menu,OnPeopleMenu] \n\![*]\q[Close,CANCEL]\e"
}


//If you write all your envelopes as lowercase, you can send them through this function whenever you need the first letter capitalized! It'd look something like this: "%(Capitalize(exampleenvelope))"
Capitalize
{
	_first = SUBSTR(_argv[0],0,1)
	_rest = ERASE(_argv[0],0,1) //Erase the first character and store everything else in _rest
	TOUPPER(_first) + _rest
}

//thanks zi! do Shuffle('string') or Shuffle(AnArray)
Shuffle
{
    _output = IARRAY
    if GETTYPE(_argv) == 4 //If it's an array. General purpose arrays only!
    {
        while ARRAYSIZE(_argv) > 0
        {
            _rand = RAND(ARRAYSIZE(_argv))
            _output ,= _argv[_rand]
            _argv[_rand] = IARRAY
        }
    }
    else //If it's anything else it'll be treated as a string
    {
        _argv = TOSTR(_argv)
        while STRLEN(_argv) > 0
        {
            _rand = RAND(STRLEN(_argv))
            _output += SUBSTR(_argv,_rand,1)
            _argv = ERASE(_argv,_rand,1)
        }
    }
    _output
}

//Searches the start of a string for the requested substring.
//Argument 0 is the source string to search, argument 1 is the substring to search for.
//Returns 1 if true, 0 if false.
//Usage example:
//StartsWith("Testing","Test")
StartsWith
{
	//Check the length of the substr to test for, check that many characters at the beginning of the string, and see if it matches the given substr
	_teststr_len = STRLEN(_argv[1])
	SUBSTR(_argv[0],0,_teststr_len) == _argv[1]
}

//Searches the end of a string for the requested substring.
//Argument 0 is the source string to search, argument 1 is the substring to search for.
//Returns 1 if true, 0 if false.
//Usage example:
//EndsWith("Testing","ing")
EndsWith
{
	//Check the length of the substr to test for, check that many characters at the end of the string, and see if it matches the given substr
	_teststr_len = STRLEN(_argv[0])
	_substr_len = STRLEN(_argv[1])
	SUBSTR(_argv[0],_teststr_len - _substr_len,_substr_len) == _argv[1]
}

//As the name implies, this checks if the balloon is currently open or not.
//Will be 0 if the balloon is not open, and 1 if the balloon is open
BalloonCheck
{
	"balloon" _in_ var.req.value[ASEARCH("Status",var.req.key)]
}

//This function allows you to add a line break, but only for the balloon you intend your ghost to use! So if a bit of text wraps awkwardly, but you don't want to add a \n because it may look super weird in other balloons, you can put a %(br) instead! It'll be a \n in your balloon, and a single space in any other balloon. Make sure you add the name of your balloon here!
//Example: "This dialogue would cut awkwardly in a hypothetical default%(br)balloon,\w4 so a manual linebreak is added for only that balloon.\w8\w8 In any other balloon this would be a%(br)continuous line,\w4 and use SSP's default word wrap.\e"
br
{
	if SHIORI3FW.BalloonName == "Hollerfeed Lite"; "\n"
	else; " "
}

//
//basically the name cleaning function in OnTitle and OnName. returns the cleaned term!!
CleanTerm
{	
	_term = _argv[0] //putting the raw input into _term to do some tests on it
	_term = TOLOWER(_term) //changes it to lowercase
	_term = REPLACE(_term," ","") //removes spaces
	_term = REPLACE(_term,".","") //removes periods
	_term = REPLACE(_term,"-","") //removes dashes
	_term
}
//

//— Flag System —
//Usage: This is intended to take the place of single-use variables that only contain 0 or 1, used as flags for certain events and such. With these functions, you can store all those flags as a single array, meaning you can make as many flags as you want without having to really worry about save bloat. It can also make your code a bit more readable!

//You'll want to put 'FlagArray = IARRAY' into your OnFirstBoot. That'll set up an empty array for flags to be stored in.
//When you want to check if a flag is set or not, you can do it like so:
//if Flag("My Flag") == 0
//or
//if Flag("My Flag") == 1

//Any flags that are NOT in the array are 0, and any flags that ARE in the array are 1. So, to set a flag to 1, you add it to the array like this:
//AddFlag("My Flag")

//If you want to set a flag back to 0, you can remove it like so:
//RemoveFlag("My Flag")

//Flag names can be just about anything you want! They're just stored as strings. For example, for S I have "Saw Gaster with googly eyes", and for Lulo I have things like "Unlocked MP3 player" and "Unlocked journal". If you want to see what flags are set, you can just put %(MiscFlags) into script input, so it's helpful to have names that describe the flag!

//Checks to see if a flag exists. Returns 1 if yes, returns 0 if no
Flag
{
	if !ISVAR("MiscFlags"); MiscFlags = IARRAY
	if ASEARCH(_argv[0],MiscFlags) != -1; 1
	else; 0
}

//Adds a flag to the array
AddFlag
{
	if !ISVAR("MiscFlags"); MiscFlags = IARRAY
	if !Flag(_argv); MiscFlags ,= _argv
}

//Removes all instances of a flag in the array
RemoveFlag
{
	if !ISVAR("MiscFlags"); MiscFlags = IARRAY
	_elements = ASEARCHEX(_argv[0],MiscFlags)
	if ARRAYSIZE(_elements) > 0
	{
		for _i = ARRAYSIZE(_elements) - 1; _i >= 0; _i-- //Goes from the end of the list to the start, so the elements stay in the same positions while we erase
		{
			_current = _elements[_i]
			MiscFlags[_current] = IARRAY
		}
	}
}

//--EVENT FLAGS
//Checks to see if a flag exists. Returns 1 if yes, returns 0 if no
EFlag
{
	if !ISVAR("EventFlags"); EventFlags = IARRAY
	if ASEARCH(_argv[0],EventFlags) != -1; 1
	else; 0
}

//Adds a flag to the array
AddEFlag
{
	if !ISVAR("EventFlags"); EventFlags = IARRAY
	if !EFlag(_argv); EventFlags ,= _argv
}

//Removes all instances of a flag in the array
RemoveEFlag
{
	if !ISVAR(EventFlags); EventFlags = IARRAY
	_elements = ASEARCHEX(_argv[0],EventFlags)
	if ARRAYSIZE(_elements) > 0
	{
		for _i = ARRAYSIZE(_elements) - 1; _i >= 0; _i-- //Goes from the end of the list to the start, so the elements stay in the same positions while we erase
		{
			_current = _elements[_i]
			EventFlags[_current] = IARRAY
		}
	}
}

//Setting variables in random dialogue
//You can use this to do detailed variable changes and such in dialogue. If you only need to set one variable, I recommend using EVAL in the line of dialogue, like so:
//"Some dialogue that changes a variable.\e:eval=somevar='some value'"
OnDialogueVars
{
	case reference0
	{
		when "dave"
		{
			AddFlag("KnowDave")
		}
		when "abtvera"
		{
			abtvcount++
			if abtvcount >= 8 {
				AddFlag("FirstAbtVera")
				ERASEVAR("abtvcount")
			}
		}
	}
}