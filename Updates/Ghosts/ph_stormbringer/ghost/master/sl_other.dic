OnTranslate
{
	_talk = reference0
	
	//This is what makes %(embedded_elements) work in script input
	if reference1 == "" && reference2 == "" //If this is from the input box
	{ //send input box : no event (ref2) , no special flag (ref1)
		_talk = EVAL('"' + REPLACE(_talk,'"','""') + '"')
	}
	
	//If you want to add anything to OnTranslate, like text replacement, I would put it after this point, generally. Just make sure it's before the TOSTR(_talk)
	
	//handles pauses in texting dialogue
	//taken out bc if you dont use the right balloon it doesn't work </3
	/*
	if RE_ASEARCH("\b[10]") && !RE_ASEARCH("NOTEXTPAUSE",_talk)
	{
		_talk = REPLACE(_talk, "\n", "\w8\w8\w8\w8\w8\w8\w8\n")
	}
	*/
	//dont want to set it to the texting balloon if youre not even using the right balloon
	if SHIORI3FW.BalloonName != "Cyber Light Chat"
	{
		_talk = REPLACE(_talk, "\b[10]", "")
	}
	
	//---------------shell modes
	
	//PURPOSE: turn off motion for surfaces that have motion.
	_motions = "\s[1],\s[2],\s[3],\s[4]"
	if Flag("NoMotion")
	{
		foreach _motions; _s
		{
			_talk = REPLACE(_talk, "%(_s)", "%(_s)\![anim,pause,0]")
		}
	}
	
	//PURPOSE: change villain surfaces to raincoat human surfaces
	//hrm hrm.. get index position...
	_raincoats = "\s[5],\s[6],\s[7],\s[8]"
	if Mode > 0
	{
		_i = IARRAY
		foreach _motions; _s
		{
			//kinda roundabout but i dont remember how to do this LOL
			_i ,= ASEARCH(_s,_motions)
			_talk = REPLACE(_talk, "%(_s)", "%(_raincoats[_i])")
		}
	}
	
	TOSTR(_talk)
}

//This is just here so you can use anchors to link to websites if you want. You can link to websites with normal menu choice tags! If you do, a handy URL preview will show up in the balloon!
OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0; "\j[""%(reference0)""]"
}

//Hotkeys
OnKeyPress
{
	if reference0 == "f1"; "\![open,readme]"
	elseif reference0 == "t"; OnAiTalk
	elseif reference0 == "r"; OnLastTalk
	elseif reference0 == "f5" && SHIORI3FW.DebugMode; "\![reload,ghost]"
}

//Happens about 30 seconds after a script ends, by default, to bring them back to normal poses
OnSurfaceRestore
{
	"\1\s[-1]\0%(plasmaidle)"
	"\1\s[-1]\0%(phoneidle)"
}

//Happens when the ghost is unminimized. You *can* include dialogue, but you don't *have* to. I have included this event because if you don't, it defaults to \0\s[0]\1\s[10], which you don't want in some cases, such as if your ghost uses different surfaces or has a different number of characters.
OnWindowStateRestore
{
	"\1\s[-1]\0%(plasmaidle)"
}

//Network update url. Make sure you have a / on the end of the url or it will give a warning every time users try to update! If you don't plan to use network updates you can remove this
//IF YOU'RE USING GITHUB FOR NETWORK UPDATES: DO NOT USE github.io URLS! It will cause you pain and suffering. USE raw.githubusercontent!!!
//To get the raw.githubusercontent link super easy:
//• Open your ghost's repo online
//• navigate to the ghost's folder (the top level one, with install.txt, readme.txt, etc)
//• Click on install.txt, or another text file on that level
//• Once on that page, click the "raw" button. You should see the url now starts with raw.githubusercontent
//• Copy that url exactly as is, but *leave off the "install.txt" or whatever file name at the end.*
//Tada! That's your url! Make sure you test it! The dev palette and script/network log can help with that
On_homeurl {"https://raw.githubusercontent.com/WhatAPhantasia/UkaHub/master/Updates/Ghosts/ph_stormbringer/"}


//—————————————————————————————— Right click menu links ——————————————————————————————

//Takes an array of alternating names and urls, and formats them for use with the right click menu
FormatLinks : all
{
	for _i = 0; _i < ARRAYSIZE(_argv); _i++
	{
		_argv[_i] //Add the link/title
		//Alternate between adding %ASC(1) or %ASC(2)
		if _i % 2 == 1; "%ASC(2)"
		else; "%ASC(1)"
	}
}

On_sakura.recommendsites {FormatLinks(recommendsites_sakura)}
On_recommendrootbutton.caption {"sNO Links"}

//These are the websites linked in the top option of the sakura's right click menu. The format is simple; write the label of the link on the left followed by a semicolon, and then on the right, the url to link to. These should be separate strings.
recommendsites_sakura : array
{
	"Lumi's Toyhou.se Page"; "https://toyhou.se/18059593.lumi"
	"sNO Info G. Doc"; "https://docs.google.com/document/d/10djS_XVg944NYmU5-8ATwNrzs00s3UQuv8JNErr0FOY/edit?usp=sharing"
	"sNO Youtube Playlist"; "https://youtube.com/playlist?list=PL0LkeuRFFKdGkBagPa-jENiqZN9gVZOdV" 
	"sNO Pinterest Board"; "https://www.pinterest.com/thenats789/sno/"
	"sNO Insta Tag"; "https://www.instagram.com/explore/tags/supernaturaloccurrences/"
	"sNO Tumblr Tag"; "https://www.tumblr.com/tagged/superNatural%20Occurrences?sort=top"
}

On_sakura.portalsites {FormatLinks(portalsites_sakura)}
On_portalrootbutton.caption {"Misc Links + Credit"}

//These are the websites linked in the second option of the sakura's right click menu. Same as above
portalsites_sakura : array
{
	"Download Page"; "https://whataphantasia.github.io/projects/ukagaka/ghosts/stormbringer"
	"Changelog (G. Docs)"; "https://docs.google.com/document/d/10yy69A5hkizTPMh1mmRISIDg8M3JMfOdL9abrYmme20/edit?usp=sharing"
	"Phantasia's Website"; "https://whataphantasia.github.io/"
	""; ""
	"Simplicity v1.0.3"; "https://github.com/Zichqec/simplicity_template"
	"Ukagaka Dream Team Wiki"; "https://ukagakadreamteam.com/wiki/"
}

On_kero.recommendsites {FormatLinks(recommendsites_kero)}

//These are the websites linked in the first option of the kero's right click menu. Same as above
recommendsites_kero : array
{
	"How are you seeing this";	""
}


//—————————————————————————————— User pronoun related ——————————————————————————————

they
{
	_p = upronouns
	if _argv[0] == 1; _p = lpronouns

	_p[0]
}
them
{
	_p = upronouns
	if _argv[0] == 1; _p = lpronouns

	_p[1]
}
their
{
	_p = upronouns
	if _argv[0] == 1; _p = lpronouns

	_p[2]
}
theirs
{
	_p = upronouns
	if _argv[0] == 1; _p = lpronouns

	_p[3]
}
themself //mostly here to set up neopronouns
{
	_p = upronouns
	if _argv[0] == 1; _p = lpronouns

	_p[4]
}
//%(they) %(p('are','is'))
//You can change the name of this function if you want! I kept it short so it's fast to type
p
{
	if upronouns[5]; _argv[0] //they are
	else; _argv[1] //he is/she is
}
lp //i hate that i split them up but i dont wanna make it more complex so yeah
{
	if lpronouns[5]; _argv[0] 
	else; _argv[1]
}
//setting this up bc i do this check WAYY too many times
//%(lg('boy','girl','kid'))
lg 
{
	if lgender == "boy"; _argv[0]
	elseif lgender == "girl"; _argv[1]
	else; _argv[2]
}

/*
An example of the above setup:
"%(they) %(p('were','was')) looking for that earlier, I'll ask if %(they)'%(p('ve','s')) found it."

If using they, the %(p) functions will return the first word specified, resulting in:
"they were looking for that earlier, I'll ask if they've found it."

If not using they, for example if using she, it will return the second specified word instead, resulting in:
"she was looking for that earlier, I'll ask if she's found it."

With this function, you can quickly customize words that need to change based on whether it should be singular or plural. Just remember; the %(p) function is set up so that the first word you specify matches with the 'they' labels used on the pronoun functions. You can always switch that around if you like, too.
*/

//-------------------

//Copied from the base SHIORI files, but with the username variable added
OnNotifyUserInfo : void
{
	SHIORI3FW.UserName = reference[0]
	SHIORI3FW.UserNameFull = reference[1]
	SHIORI3FW.UserBirthday = SPLIT(reference[2],',',3)
	for _i = 0 ; _i < 3 ; _i++ {
		SHIORI3FW.UserBirthday[_i] = TOINT(SHIORI3FW.UserBirthday[_i])
	}
	SHIORI3FW.UserSex = reference[3]
}


//---------------------
//qol time stuff from x template because I LIKE IT

//This function is to help with changing text alignment in the same line! It makes a new line by using \n, displaying a space, and then going back up a line. So you'd use it like this:
//"Text on the left%(newline)\f[align,right]Text on the right"
newline {"\n \_l[0]\n[-100]"}

//How many minutes the ghost was up in total, including current session
up_total {up_all + up_mins}

//How long the ghost has been up in the current session
up_mins {(GETSECCOUNT / 60) - boot_min}

//Total hours
totalhours {up_total / 60}

//current session hours
up_hours {up_mins / 60}

//Total hours + minutes
totalruntime
{
	_hours = up_total / 60
	_mins = up_total % 60
	if _mins <= 9; _mins = "0%(_mins)"
	
	"%(_hours):%(_mins)"
}

//Current runtime as hours + minutes
runtime
{
	_hours = up_mins / 60
	_mins = up_mins % 60
	if _mins <= 9; _mins = "0%(_mins)"
	
	"%(_hours):%(_mins)"
}

//AM or PM - ampm is 0 during am, and 1 during pm
ap
{
 	if ampm == 0; "AM"
 	else; "PM"
}

//---------------------
//WEATHER TIME OGURGHEUTGHETHOJRG
//Weather station code. this is in dev resources so i hope its alright
On_installedplugin
{
    pluginlist = IARRAY
    foreach reference; _ref
    {
        pluginlist ,= _ref[0]
    }
}

WeatherStationInstalled
{
    if RE_ASEARCH("Weather Station",pluginlist) != -1; 1
    else; 0
}

//weather types:
//clear, cloudy, mightrain, mightsnow, mightthunder, raining, snowing, freezingrain, sleet, icepellets, thunderstorm, thundersnow, foghaze

//base chances:
/*
	OUT OF 100:
	clear:		22
	cloudy:		25
	mrain:		9
	msnow:		3
	mthunder:	7
	rain:		14
	snow:		3
	frain:		1
	sleet:		1
	pellets:	1
	thunder:	10
	thsnow:		1
	fog:		3
*/

//WEATHER SIMULATOR YAYYYYYYYYYY
OnVirtualWeather.Temp
{

}

OnVirtualWeather.Set
{
	//runs on boot, determines what weather the day starts off with
	//based on the weather trends of cyber light city
	
	_c = RAND(100) + 1
	
	if weatherdata[0] == ""
	{
		//if these is no pre existing weather state. this can be due to an error, or if it's never been set before.

		if weatherdata[3] == ""
		{
			if _c <= 22
			{weatherdata[0] = "Sunny"
			weatherdata[0] = "Clear"} //this doesnt actually randomize,,,,,, ill have to do a raise instead....
			elseif _c <= 47
			{}
			elseif _c <= 56
			{}
			elseif _c <= 59
			{}
			elseif _c <= 56
			{} //yeah im kinda tired ill do this later (aka never)
		}
		else
		{
			//if temp data exists which like, i have no idea how it WOULD
		}
	}
	else
	{
		//okay so this serves the purpose of changing the weather based on the previous weatherstate.
		//runs every hour or so... user can change the interval, actually...

	}
}

OnVirtualWeather.FSet
{
	//ahh geez how am i gonna do this..... i think i'll just set the weather and temperature accordingly once the day rolls over....
	// ^ see forecastrollover

	_c = RAND(100) + 1

	if weatherdata[0] == ""
	{
		//if theres no weather data im straight up gonna assume theres no OTHER weather data
	}
	elseif forecastdata[0] == ""
	{
		//runs in the case where there's no forecast data and lumi needs to make some shit up LOLL


	}
	else
	{

	}
}

OnVirtualWeather.ForecastRollover
{
	//this function is specifically for transferring the forecast data to weather data (with some margin of error) once the day rolls over
	//hmmmmmm... how will i do this.... sets itself at hour 0 and also when booting for SPECIFICALLY the next day... how do you track that? closetime? yeah...
}

OnWeatherStation.Weather
{
	weatherconfig[0] = reference35		//current unit
	
	weathererror = ""
	//weatherdata array order here
	//weather condition, humidity, day/night, temp, feel temp, wind speed, precipitation, visibility, cloud cover percentage, rain, snow
	weatherdata[0] = reference0			//current weather condition
	weatherdata[1] = TOINT(reference8)	//humidity
	weatherdata[2] = TOINT(reference3)	//day or night
	
	if weatherconfig[0] == "Metric"
	{weatherdata[3] = TOINT(reference5) //temp
	weatherdata[4] = TOINT(reference7)	//feel temp
	weatherdata[5] = TOINT(reference10)	//wind speed
	weatherdata[6] = TOINT(reference16)	//precipitation
	weatherdata[7] = TOINT(reference23) //visibility
	}
	else //Imperial
	{weatherdata[3] = TOINT(reference4)
	weatherdata[4] = TOINT(reference6)
	weatherdata[5] = TOINT(reference9)
	weatherdata[6] = TOINT(reference15)
	weatherdata[7] = TOINT(reference22)
	}
	
	weatherdata[8] = TOINT(reference17)	//cloud cover percentage
	weatherdata[9] = TOINT(reference18)	//will it rain? y/n
	weatherdata[10] = TOINT(reference20)//will it snow? y/n
	//chance of rain is in forecastdata.
}

OnWeatherStation.Forecast.Day
{
	//CALLED WITH REFERENCE0 AS THE DAY OF THE FORECAST DESIRED!! 
	//right now the default that lumi calls is 1, which is the next day.
	
	//forecast data array order here
	//rain chance, snow chance, max temp, min temp, avg temp, windspeed, precipitation, visibility, humidity
	forecastdata[0]=TOINT(reference13)	//rain chance percentage
	forecastdata[1]=TOINT(reference15)	//snow chance percentage
	
	if weatherconfig[0] == "Metric"
	{forecastdata[2]=TOINT(reference2)	//max temp, celsius
	forecastdata[3]=TOINT(reference4)	//min temp
	forecastdata[4]=TOINT(reference6)	//avg temp
	forecastdata[5]=TOINT(reference9)	//avg windspeed, kph
	forecastdata[6]=TOREAL(reference11)	//precipitation, millimeters
	forecastdata[7]=TOINT(reference17)	//visibility, kilometers.

	}
	else //Imperial
	{forecastdata[2]=TOINT(reference1)	//fahrenheit
	forecastdata[3]=TOINT(reference3)
	forecastdata[4]=TOINT(reference5)
	forecastdata[5]=TOINT(reference8)	//mph
	forecastdata[6]=TOREAL(reference10)	//inches
	forecastdata[7]=TOINT(reference16)	//miles
	}
	
	forecastdata[8]=TOINT(reference7)	//humidity
}

//i cant do hourly i think i'll dieee
//TODO: hourly weather for a later update~
/*
OnWeatherStation.Forecast.Hourly
{

}
*/
ResetWeatherData
{
	weathererror = ""
	weatherdata = ("","","","","","","","","")
	weatherconfig = ("")
	forecastdata = ("","","","","","","","","")
	forecastdata.hour = ("","","","","","","","","")
}

OnWeatherStation.Error
{
	ResetWeatherData
	weathererror = reference0
}

GetWeatherData : all
{
	if WeatherStationInstalled
	{"\![raiseplugin,Weather Station,OnWeatherStation.Weather]"
	"\![raiseplugin,Weather Station,OnWeatherStation.Astro]"}
	else
	{
		//OnVirtualWeather.Set
	}
}

OnGetForecastData
{
	if WeatherStationInstalled
	{"\![raiseplugin,Weather Station,OnWeatherStation.Forecast.Day,1]"}
	else
	{
		//OnVirtualWeather.FSet
	}
}

//---------------------
//hemisphere: check if weather plugin is installed and location data is set, and set hemisphere to that. if not, make manual.

//same thing for seasons

daytime 
{
	if WeatherStationInstalled 
	{GetWeatherData
	weatherdata[2]}
	else
	{if hour > 7 && hour < 19; 1
	else; 0}
}

//used for the day/night dialogues....
dn
{
	if daytime; _argv[0]
	else; _argv[1]
}

timeslot
{
	case hour
	{
		when 5-8; "earlymorning" //5-8am
		when 9-11; "morning" //9-11am
		when 12-14; "lunch" //12-2pm
		when 15-17; "afternoon" //3-5pm
		when 18-20; "evening" //6-8pm
		when 21-23; "latenight" //9-11pm
		others; "midnight" //12-4am
	}
}

Capitalize
{
	_buffer = SUBSTR(_argv[0],0,1) //Storing the first character in _buffer
	_argv[0] = ERASE(_argv[0],0,1) //Erasing the first character from _argv[0]
	_argv[0] = INSERT(_argv[0],0,TOUPPER(_buffer)) //Making the character in _buffer uppercase and inserting it back into _argv[0]
	_argv[0] //Returns the capitalized word
}

//----------FLAG CODE, by zi

//Checks to see if a flag exists. Returns 1 if yes, returns 0 if no
Flag
{
	if !ISVAR("MiscFlags"); MiscFlags = IARRAY
	if ASEARCH(_argv[0],MiscFlags) != -1; 1
	else; 0
}

//Adds a flag to the array
AddFlag
{
	if !ISVAR("MiscFlags"); MiscFlags = IARRAY
	if !Flag(_argv); MiscFlags ,= _argv
}

//Removes all instances of a flag in the array
RemoveFlag
{
	if !ISVAR("MiscFlags"); MiscFlags = IARRAY
	_elements = ASEARCHEX(_argv[0],MiscFlags)
	if ARRAYSIZE(_elements) > 0
	{
		for _i = ARRAYSIZE(_elements) - 1; _i >= 0; _i-- //Goes from the end of the list to the start, so the elements stay in the same positions while we erase
		{
			_current = _elements[_i]
			MiscFlags[_current] = IARRAY
		}
	}
}