//This file handles a couple little QOL functions you may want. But these are not necessary! If you don't care for them just erase them. If you don't want to set up network updates, you can comment or remove the update functions too. If you're not interested in any of these, just remove the whole file!

//—————————————————————————————— Installing something ——————————————————————————————
OnInstallComplete
{
	"\0\s[1]Ooh! \w8Looks like we've got a %(reference0),\w4 somethin' called %(reference1)."
	--
	if "ghost" _in_ reference0 //A little menu to open or change to the ghost that was installed immediately
	{
		"\n\n\_q"
		--
		"\![*]\__q[OnInstallComplete.Action,call]Call %(reference0)\__q  \![*]\__q[OnInstallComplete.Action,change]Change to %(reference0)\__q\n\n"
		--
		"\f[align,right]\![*]\q[THANKS!,blank]"
	}
}

OnInstallComplete.Action
{
	if reference0 == "change"
	{
		"\![change,ghost,lastinstalled]"
	}
	else //call
	{
		"\![call,ghost,lastinstalled]"
	}
}


//—————————————————————————————— Creating a .nar file ——————————————————————————————
OnNarCreated
{
	_nar = "\_a[OnNarLocation,""%(reference1)""]%(reference0)\_a" //Makes a clickable link that'll open the location of the file they just made
	
	"\0\s[1]I've got your %(_nar) file right and packaged!"
}

OnNarLocation
{	
	_path = SPLITPATH(reference0)[0] + SPLITPATH(reference0)[1]
	
	"\![open,file,""%(_path)""]"
}


//—————————————————————————————— Network Update ——————————————————————————————
//Starting an update
OnUpdateBegin
{
	"\0\s[1]\i[1]\i[33]Ooh, do we got mail?\w8"
}

//reference0 is the number of new files, starting from 0
OnUpdateReady
{
	reference0++ //Number of files starts from 0, so this displays the correct amount
	
	//plural checks, adds an s and changes is to are if there's more than 1 file
	_s = ""; _are = "is"
	if reference0 != 1; {_s = "s"; _are = "re"}
	
	"\0\s[1]\i[1]\i[33]Haha, yay! \w8There'%(_are) %(reference0) new file%(_s) for us!\w8"
}

//When the update finishes. Don't forget to initialize any new variables you've created! I highly highly recommend using OnInitialize for this (it's in boot.dic), in case the user updates via the ghost explorer or some other means.
//reference0 is 'none' if there were no new files to update with, reference1 is a list of new file names, separated by commas
OnUpdateComplete
{
	if reference0 == "none"
	{
		"\0\s[1]\i[1]Aw, what? \w8Turns out there's nothing..."
	}
	else
	{
		"\0\s[1]\i[1]\i[33]Yay, done! \w8\![anim,stop,1]\i[4]Can't \w4WAIT\w4 to dig through all this new stuff!"
	}
}

//reference0 is the reason the update failed
OnUpdateFailure //If the update fails
{
	if reference0 == "timeout" //If it timed out
	{
		"\0\s[2]\i[1]Hm? \w8\i[32]Eh, think the mail timed out on us."
	}
	elseif reference0 == "md5 miss" //If the update files have not been uploaded correctly and don't match the updates2.dau
	{
		_file = SPLITPATH(reference1)[2] + SPLITPATH(reference1)[3] //This gets the name and extension of the offending file
		
		"\0\s[2]\i[1]\i[51]Aw, dang! \w8MD5 files don't match! \w8(Contact Phantasia and let nim know that '%(_file)' is giving an MD5 error!)"
	}
	elseif reference0 == "artificial" //If the user cancels the update by double clicking the balloon
	{
		"\0\s[2]\i[4]Oh, whoop. \w8Let's not read that, actually."
	}
	else //Any other errors
	{
		"\0\s[2]\i[1]...Hm. \w8No idea what THIS means. \w8""%(reference0)""?"
	}
}