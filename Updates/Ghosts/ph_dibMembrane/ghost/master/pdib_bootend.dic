//---------------------------Booting and Ending--------------------------

//This .dic file is set aside for booting and ending dialogue, as well as a few other functions. Make sure to read the walkthrough (http://www.ashido.com/ukagaka/walkthrough.html).


//levidre i am so sorry for stealing basically all my font code from zim :sob: idk how to code

OnNotifyBalloonInfo
{
	balloonname = "%(reference0)"
	balloonpath = "%(reference1)"
}

OnBalloonChange
{
	balloonname = "%(reference0)"
	balloonpath = "%(reference1)"
}

OnNotifyFontInfo
{
	_i = SAORI("saori\fontinstaller","get","Irken Normal (TrueType)")
	if "%(_i)" != "0" && hastranslator == -1
	{
		hastranslator = 0
	}
}

//--OnFirstBoot--

OnFirstBootVar
{
	hastranslator = 0
	//lookdirection = 1
	//nowkeroname = "none"
	
	lastTalk = ""
	nowshell = "Shooting Star" //name of shell in descript.txt, case sensitive i believe
	username = "you"
	pronouns = "undef"
	custompronouns = ("ey","em","eir","eirs")
	pronounplur = "plur"
	mikireflag = 0
	birthday[0] = "???"	 //The user's birthday, written out with words
	birthday[1] = "???"	 //The user's birthday, written in DD/MM format
	talkrate = "every 5 minutes"
	aitalkinterval = 300
	totalpunches = 0
	nowscale = (100,100) //Current scale, as a percentage
	teachusername = 0
	//---Petting related variables---
	stroke[0] = 0		 //The stroke value, which resets after each pet
	stroke[1] = 0		 //The stroke value, but it doesn't reset after each pet
	stroke[2] = 0		 //The current streak of successful pets
	pethighscore[0] = 0	 //The petting high score, as the number of the stroke value
	pethighscore[1] = 0	 //The petting high score, as the number of pets
	totalpets = 0		 //The total times the ghost has been pet
	offscreen = IARRAY
	//---
	deleteshitai = "OFF"
	//AutoUpdates = ("On",0)
	//nowmode = "Default" //Uncomment if you plan to add more modes
	installdate = ("%(year)","%(month)","%(day)","%(hour)","%(minute)","%(second)","%(ghostver)") //This tracks the exact day and time the ghost was installed, in case you want to mess around with that later. It also tracks what version the user installed, which may be helpful for debugging!
	MiscFlags = IARRAY	 //For the flag system
	
	x.boot_min = GETSECCOUNT / 60 //What time (epoch) the ghost was booted
	x.up_all = 0 //How many minutes the ghost was up for in total, not including current session
}

//How many minutes the ghost was up in total, including current session
x.up_total {x.up_all + x.up_mins}

//How long the ghost has been up in the current session
x.up_mins {(GETSECCOUNT / 60) - x.boot_min}

OnFirstBoot
{
	uninstallStatus = reference0
	firstboot = 1
	OnFirstBootVar
	OnDiscordPluginBoot
	--
	"\![enter,passivemode]\1\s[-1]\0\s[-1]"
	--
	_i = SAORI("saori\fontinstaller","get","Irken Normal (TrueType)")
	if "%(_i)" == "0"
	{
		"The Irken font isn't installed. Some dialogues need this font. Do you want to install it?\n\n[half]\![*]\q[Yes,installirken]     \![*]\q[No,nofont]"
	}
	else
	{
		chooseshell
	}
}

chooseshell
{
	"\_q(Do you want to use the Shooting Star shell or the Florpus shell?)\n\n\![*]\q[Shooting Star,fanonplease] - A more fanon look, very tall\n\![*]\q[Florpus,canonplease] - More ETF adjacent, looks nice next to Levidre's Zim ghost\n\n(You can change how he looks later in the right click menu under Change Shells.)\_q"
}

Select.fanonplease
{
	"\![change,shell,Shooting Star]"
}

Select.canonplease
{
	"\![change,shell,Florpus]"
}

Select.installirken
{
	_i = SAORI("saori\fontinstaller","install","Irken Normal (TrueType)",balloonpath + "Irken.ttf")
	if "%(_i)" == ""
	{
		"(Font installed.)\n\n[half]"
	}
	else
	{
		if "%(_i)" != ""
		{
			hastranslator = -1
			"(There has been an issue installing the Irken font : %(_i))\n(You can install it manually with the files in the Swollen Eyeball balloon. All irken dialogue will be locked until the font is installed.)\x"
		}
		else
		{
			hastranslator = 0
			"\n[half]\x"
		}	
	}
	--
	if firstboot == 1
	{
		chooseshell
	}
}

Select.nofont
{
	"(Any dialogue that has irken text will not appear until the font is downloaded.)\n\n[half]"
	hastranslator = -1
	--
	if firstboot == 1
	{
		chooseshell
	}
	else
	{
	}
}


OnFirstBootTalk
{
	if uninstallStatus == 0
 	{
		//temp expressions: replace with actual static poses w/ spelldrives in post
		"\1\s[-1]\0\s[0]\i[79]\i[92]\i[23]Ah- \w4It worked! The spelldrives still work!! I%(br)can't believe I'm... \w2Uh, wherever here is! \w8Wait, what does Digital Jump mean, anyway? And- \w2Why did it only cost two power points? \w4\xOh, nevermind that!! \w2This looks like... \w4A desktop! \w6Pretty cool... \w5\w5If this is a desktop, then it must belong to someone, right? \w8\n.\w2.\w2.\w8\w8\n\n[half]Oh. \w5Hi! \w8I've just been \w2talking to myself this whole time, I didn't even notice you were- \w4Whatever! \w8\xWhat's the name of the person whose computer I'm on, anyway?\n\n[half]"
		--
		"\w5\_q\![open,inputbox,OnNameTeach,-1]\_l[-10000,-10000]\*\q[ ,CANCEL]"
 	}
	else
 	{
		//---- You'll see this if you try to run OnFirstBootTalk using ![raise] without the extra 0 as mentioned above. except i cant actually get it to work?? prob bc i separated onfirstboot and onfirstboottalk lmaoooooooo there's no set reference0 for this function
 		"\1\s[-1]\0\s[0]Aha! Now- \w8Wait, here again? \w5I guess I should have expected that... \w8\n\n[half]Hey again. \w2I mean, \w1\f[bold,1]are\f[bold,0]\w1 you the same person? \w2What's your name?\n\n[half]"
		--
		"\w5\_q\![open,inputbox,OnNameTeach,-1]\_l[-10000,-10000]\*\q[ ,CANCEL]"
 	}
}


//-----------------------Normal Booting----------------------------

//These are new variables to initialize after an update. By checking if the variable doesn't exist before initializing it, we prevent variables already in use from being overwritten. This is VERY IMPORTANT, or you risk resetting the user's progress on your new variables every time they update.
//ISVAR is a built in YAYA function that checks if a variable exists(is initialized) or not. It returns 0 if the variable doesn't exist, and 1 if it does.
UpdateVars
{
	FixDefaultOptions //Putting this here too, since the variable issues I've had seemed to happen mostly when updates went wrong!
	//if ISVAR("examplevariable") == 0; examplevariable = 0
}

OnBootVar
{
	FixDefaultOptions
 	lastTalk = "" //This is part of how chained conversations work (see aitalk.dic).
	x.boot_min = GETSECCOUNT / 60 //What time (epoch) the ghost was booted
}
OnBoot
 {
	OnBootVar
	OnDiscordPluginBoot
	--
	OnBootPhaseOne
	//temppose = OnBootPhaseOne
	//OnUpdateCoordinates
}
OnBootPhaseOne
{
 	"\0\s[0]" //This sets them up in their neutral pose for now. "\1%(keroneutral)"
	
	//put %(normalboottalk) if u dont want indiv day messages
	//"\0\s[0]\1\s[10]%(normalboottalk)"
	--
	if "%(month)/%(day)" == birthday[1]
	{
		"\0\s[0]It's %(bornmonthprint) %(borndayprint). \w4I've kinda been looking forward to today, cause it's your birthday, right? \w8Happy birthday, %(username).\e" 
		--
		if "%(month)/%(day)" == "3/30"
		{
			"\x...Oh, and it's my birthday today, too. Nice! (Later im gonna make this jump to it's own Dib Birthday function, yeah?)" 
		}
	}
	elseif "%(month)/%(day)" == "3/30" 
	{
		"\0\s[0]I'm going to code in some cute birthday boot conversation here!! Not yet though. Surprise!\e"
	}
	else
	{
		NormalBootTalk
	}
}

NormalBootTalk : pool
{
	{
	"\0\s[23]\i[72]\i[78]\i[96]-So that's why "
	--
	"Mothman-! "
	"Bigfoot-! "
	"Flatwoods-! "
	"Owlman-! "
	"aliens-! "
	--
	"\w8\w2\s[3]\i[96]\w8\w2...Ohh, \w5um. \w8\w2\s[2]\i[98]\i[77]Hey, \w5%(username)!\e"
	}
	"\0\s[21]\i[92]\i[21]\w8\w8\i[201]...Oh. \w8\w6\s[2]\i[91]Back on the computer again?\e"
	"\0\s[0]...Oh. \w8Darn! \w6%(username), I was doing some very important paranormal investigator stuff! \w2Guess I can still do it here, but...\e"
	
	if timeslot == "earlymorning"
	{
	"\0\s[0]This is an early morning boot dialogue. \w8Between the hours of five and eight.\e"
	}
	elseif timeslot == "morning"
	{
	"\0\s[0]This is a morning boot dialogue.\e"
	"\0\s[0]This is another morning boot dialogue.\e"
	}
	elseif timeslot == "lunch"
	{
	"\0\s[0]This is a lunch boot dialogue.\e"
	"\0\s[0]This is another lunch boot dialogue.\e"
	}
	elseif timeslot == "afternoon"
	{
	"\0\s[0]This is an afternoon boot dialogue.\e"
	}
	elseif timeslot == "evening"
	{
		if weekday != 7 || weekday != 0 //TODO: another skool schedule thing
		{
		"\0\s[21]\i[21]...\w6\s[21]Oh, \w4hey %(username). \w8\s[0]\i[91]Suppose here isn't a bad place to spend some time after skool.\e"
		}
	"\0\s[21]\i[21]...\w6\s[21]Oh, \w4hey %(username). \w8\s[0]\i[91]Suppose here isn't a bad place to spend an evening.\e"
	}
	elseif timeslot == "latenight"
	{
	"\0\s[0]...Oh. \w8Hey, %(username)! \w8Shouldn't you be getting to sleep soon?\e"
	"\0\s[21]\i[70]\i[21]\i[92]-And move that over there... \w8\i[201]\i[203]Huh? \w8Oh, \w5%(username)? \w8\s[0]\i[91]\i[76]Didn't know you'd call me around this time. \w8\s[2]\i[91]\i[21]Guess I'll continue my research here, then!\e"
	}
	elseif timeslot == "midnight"
	{
		if weekday == 1
		{
			"\0\s[0]Oh... You're up, too? \w4Don't know why I'm doing this to myself... It's a Skoolday tomorrow. \w2Or today?\w8"
		}
	"\0\s[0]Mnnhuh? \w2Oh. \w4Hey, %(username)...\w8"
	}
}

//These are variables to set when the ghost is closing or switching away, similar to the above.
CloseVars
{
	lastclosed = ("%(year)","%(month)","%(day)","%(hour)","%(minute)","%(second)") //This tracks the last time the ghost was closed.
	x.up_all = x.up_total //I rewrote the timekeeping system... I like the way it's set up in the base SHIORI files, except that it goes back to 0 every time the ghost reloads and that's annoying. So I recreated it lol
}
//TODO: buff up the whole "goodbye" dialogues later yea?
OnClose : pool
{
	CloseVars
	
	if x.up_mins >= 5 //If the ghost has been open longer than 5 minutes.
		{
			"\0\s[0]Seeya, %(username)!\w8\w8"
			
			if timeslot == "earlymorning"
			{
				"\0\s[0]Up and at 'em, then? \w6Bye.\w8\w8"
			}
			elseif timeslot == "morning"
			{
				if weekday != 0 || weekday != 6
				{
					"\0\s[0]Aw, darn. \w1I was hoping I could skip class today.\w8"
					"\0\s[0]Right in the middle of class... I wonder what ZIM's planning right now. \w2Nothing good, probably.\w8"
				}
				"\0\s[0]Up and at 'em, then? \w6Bye.\w8\w8"
			}
 			elseif timeslot == "lunch"
			{
				if weekday != 0 || weekday != 6 //TODO: replace with more accurate skool schedule tracker
				{
					"\0\s[0]Aw, darn. \w1I was hoping I could skip class today.\w8"
					"\0\s[0]Oh, nice! \w2I can still check in on ZIM at Skool...\w8"
				}
				"\0\s[0]Get something to eat if you can, yeah?\w8"
			}
 			elseif timeslot == "afternoon"
			{
				if weekday != 0 || weekday != 6
				{
					"\0\s[0]Oh, dang, it's already %(nowhour) PM? \w6Looks like I don't have to go to Skool...\w8"
				}
				"\0\s[0]Logging off, then? \w8"
			}
  			elseif timeslot == "evening"
			{
				if weekday == 6 //if its set up like this and not how lunch and morning are set up it'll make the ifs the only possible messages.
				{
					"\0\s[0]Got something to do on a Saturday evening? \w7Seeya, %(username).\w8"
				}
				else
				{
					"\0\s[0]Oh, it's already this late..? \w7See you later then, %(username).\w8"
				}
			}
			elseif timeslot == "latenight"
			{
				if weekday == 6 //This is the same check as above to see if it's Saturday. Note that this doesn't have an else statement! Sometimes you can get away with that, but make sure you test thoroughly.
				{
					"\0\s[0]Alone on a Saturday night? \w6Haha, no, I'm not going to make that joke. \w6Bye, %(username)!\w2\w8"
				}		
				"\0\s[0]%(dibarmfiddle)Logging off for the night? \w6\s[2]\i[98]Seeya, then.\w8"
				"\0\s[0]Oh! \w8\s[2]\i[98]Sweet dreams, %(username). \w8\w2\i[77]\w5...If you're planning to sleep, that is.\w8\w4"
				"\0\s[2]%(dibarmfiddle)Good night, \w2then.\w8"
			}
			elseif timeslot == "midnight"
			{
				if weekday == 0 //checking if it's the middle of the night on Sunday
				{
					"\0\s[0]\i[77]Yeesh, do you have anything tomorrow? \w2\s[0]\i[25]\i[77]Or today, \w4I guess. \w8\s[2]\i[77]You should get some rest.\w8\w2"
				}	
				"\0\s[0]\i[77]\i[91]I hope you're planning on getting some sleep if you haven't already, %(username). \w6\s[22]\i[92]\i[80]\i[77]\i[21]\i[70]Though I guess I shouldn't be a hypocrite...\w8\w4"
			}
		}
 		
 	else //This dialogue is for if they haven't been open for at least five minutes.
 	{
 		"\0\s[0]That was short. Did you get me by accident?\w8\w8"
 		"\0\s[0]Uh, okay. See you!\w8\w8"
 	}
 	--
	"\-\e" //dont forget that \- to close the ghost you idiot
}


 //-------------------------State Changing--------------------
 //From here on, most of the dialogue will have to do with changing shells, ghosts, or states in some way.
 

OnWindowStateMinimize
{
	"\0\s[-1]...Looks like he's been put into the taskbar. \w8You can bring him back out by clicking on his tab.\w8\w8\w4\e"
}

OnWindowStateRestore
 {
	"\0\s[0]\w6Back out.\e"
	"\0\s[0]\w6Y'know, \w6being on a computer is a very interesting experience.\e"
	"\0\s[0]\w6Woah! \w6It's kinda cramped down there...\e"
}


//--OnGhostChanging

//This runs whenever you change or reload the ghost. You can change ghosts by going into the right click menu and going to Change Ghost, but this dialogue will also be called if you reload them using Utilities->Reload Ghost or the Developer's Console->Reload->Ghost. Protip: If you are going to make a ghost, you will be reloading it A LOT.
//Note: If you reload your ghost using the developer console instead of Utilities->Reload Ghost, it won't do this dialogue, so don't freak out if it doesn't show up.
 
OnGhostChanging
 {
	_path = REPLACE(SHIORI3FW.Path,"\","/")
	_path = REPLACE(_path,"ghost/master/","")
	
	--
	
	if reference3 == _path 
	{
		"\0\s[2]\i[91]\i[77]Ely, you are a mess. \w4Fix me already! \w2\i[65](also change this dialogue later lmao)\w8\w8\e"
		"\0\s[0]\i[76]\i[91]How many times are you gonna reload me today?\w8\w8\e"
		"\0\s[2]\i[96]\i[73]Let's play 'Will Dib Be Broken When You Reload Him Or Not?'\w8\w8\e"
		"\0\s[0]\i[91]\i[70]Okay, okay, let's just get this over with.\w8\w8\e"
	}
	elseif reference0 == "Zim"
	{
		"\0\s[3]\i[70]\i[91]Ugh, ZIM? \w4Have fun, I guess.\w8\w8\e"
		"\0\s[3]\i[91]\i[29]ZIM?! \w5\0\s[3]\i[91]\i[78]Don't tell him anything that could help him defeat me!!\w8\w8\e"
	}
	elseif reference0 == "Dib" //change to whatever quo or anyone elses dib has as their sakura name
	{
		"\s[0]\i[91]\i[76]\i[21].\w2.\w2.\w2Huh. \w8\w6\i[201]Was that me just now?\e"
	}
	elseif reference2 == "Ely Crowe"
	{
		"\0\s[0]Oh... \w8\s[2]Ely! \w8Say hi to them for me!\w8\w8\e"
	}
	elseif reference0 == "Snatcher"
	{
		"\s[11]\i[94]\i[28]The Snatcher, huh? \w2An actual ghost... Shadow... \w4\s[2]\i[94]Thing! \w6That's going to be interesting. \w8\w8\n\n\s[2]\i[93]\i[21]\i[77]This is a little out of character, \w2\s[12]\i[73]\i[93]but thanks for downloading him!"
	}
	elseif reference2 == "Minimum YAYA Template" || ("GT Template" _in_ reference2) ||  reference0 == "X. Template"
	{
		"\s[2]\i[4]\i[94]%(reference2)? \w8Making something? \w4Good luck.\w\w\e"
	}
	elseif reference2 == "Needle" || reference2 == "Radio Nowhere" || reference2 == "Paper" || reference2 == "Hydrate" || reference2 == "Balloons" || reference2 == "Pet Rock" || reference2 == "Plushie Base"
	{
		"\s[0]\i[91]%(reference0)? \w8\s[0]\i[70]\i[91]\i[76]Okay, now you're just bringing objects onto here. \w6\s[11]\i[92]\i[76]What \w2\f[bold,1]is\f[bold,0] \w3the extent of things you can put on your desktop, anyway?\w8\w8\e"
	}
	elseif reference2 == "S the Skeleton" || reference2 == "Multiverse Sans" || reference2 == "Dr. W.D. Gaster" || reference2 == "sans" || reference2 == "Sans" || reference2 == "Papyrus" || reference2 == "Dreamtale Sans" || reference2 == "Jazz"
	{
		"\s[12]\i[94]Oh, wow, a skeleton! \w6\i[28]%(reference2)? \w6It's been a while since I messed with the undead... \w8\s[2]\i[93]\i[77]\i[21]\i[80]Last time didn't go so well.\w8\w8\e"
	}
	elseif reference2 == "FLUX" || reference2 == "flele" //TODO: check if flele is actually correct
	{
		"\s[0]Oh, %(reference0) looks like they can play music. \w8\w3...I have a music player too, %(username)! \w2With cool playlists! \w5Am I not enough for you? Why do you need another music player in your life?! I WANT TO BE THE ONLY ONE WHO PLAYS MUSIC FOR YOU!!!\n\n[half]\w8\w3Nah I'm just kidding. \w4Phantasia didn't expect anyone to see this so they're having fun lmao\w8\w8\e"
	}
 	else
	{
		"\0\s[0]\i[91]Hmm... %(reference0)? Interesting!\w8\w8\e"
		"\0\s[0]\i[92]\i[76]How did %(reference0) get here? \w3I doubt they have spelldrives.\w8\w8\e"
		"\0\s[0]\i[92]\i[76]Hm, %(reference0)? \w6\0\s[0]\i[96]\i[76]Your desktop seems to be a popular hangout place.\w8\w8\e"
	}
}


//--OnGhostChanged

//A sister to the above, this runs after the ghost has either reloaded itself, or when you switch from another ghost to this ghost.

OnGhostChanged
{
	_path = REPLACE(SHIORI3FW.Path,"\","/")
	_path = REPLACE(_path,"ghost/master/","")
	
	"\0\s[0]" //this sets up their default poses "\1%(keroneutral)"
	--
	if reference3 == _path  
	{
		"\s[2]\i[96]Better?\w8\e"
		"\s[0]\i[91]Code all sorted?\w8\e"
		"\s[0]Back again. \w6\0\s[2]\i[96]Hey, %(username)!\w8\e"
		//me specific dialogue
		"\s[0]\i[76]\i[91]...How many times have you looped that ZIM cover of Situations? \w8\0\s[2]\i[78]\i[91]It's hilarious, keep it on.\w8\e"
		"\s[14]\i[24]Uh, %(username)... \w8\w8\w2\0\s[11]\i[24]\i[76]Where did you get that song? \w8And how many times have you looped it?\w8\w4\n\n[half]\0\s[21]\i[21]\i[92]\i[70]I'll admit, his singing voice is...\w8\e"
		"\s[22]\i[93]\i[72]On my touch-tone, touch-tone telephone...\w8\e"
	}
	else
	{
		OnBootVar
		OnDiscordPluginBoot
		--
		if reference0 == "Zim" //should be the name of ghost as in install.txt or descript.txt.
		{
			"\s[0]\i[91]\i[76]Hey, ZIM was just here! \w6\0\s[12]\i[76]\i[92]You think you can tell me his weaknesses?\e"
		}
		elseif reference0 == "Dib" //change to whatever quo or anyone elses dib has as their sakura name
		{
			"\s[0]\i[91]\i[76]\i[21].\w2.\w2.\w2Huh. \w8\w6\i[201]Was that me just now?\e"
		}
		elseif reference0 == "Ely"
		{
			"\s[22]\i[77]\i[93]\i[27]\i[85]AAA ely um... ahem... theyre so... cute :flushed:\e" //change this later lol
		}
		elseif reference0 == "Snatcher"
		{
			"\s[11]\i[94]\i[28]The Snatcher, huh? \w2An actual ghost... Shadow... \w4\s[2]\i[94]Thing! \w6That's going to be interesting. \w8\w8\n\n\s[2]\i[93]\i[21]\i[77]This is a little out of character, \w2\s[12]\i[73]\i[93]but thanks for downloading him!"
		}
		elseif reference2 == "Minimum YAYA Template" || ("GT Template" _in_ reference2) ||  reference0 == "X. Template"
		{
			"\s[2]\i[4]\i[94]%(reference2)? \w8Making something? \w8\i[73]The more people on here, the merrier."
		}
		elseif reference2 == "Needle" || reference2 == "Radio Nowhere" || reference2 == "Paper" || reference2 == "Hydrate" || reference2 == "Balloons" || reference2 == "Pet Rock" || reference2 == "Plushie Base"
		{
			"\s[0]\i[91]%(reference0)? \w8\s[0]\i[70]\i[91]\i[76]Okay, now you're just bringing objects onto here. \w6\s[11]\i[92]\i[76]What \w2\f[bold,1]is\f[bold,0] \w3the extent of things you can put on your desktop, anyway?"
		}
		elseif reference2 == "S the Skeleton" || reference2 == "Multiverse Sans" || reference2 == "Dr. W.D. Gaster" || reference2 == "sans" || reference2 == "Sans" || reference2 == "Papyrus" || reference2 == "Dreamtale Sans" || reference2 == "Jazz"
		{
			"\s[12]\i[94]Oh, wow, a skeleton! \w6\i[28]%(reference2)? \w6It's been a while since I messed with the undead... \w8\s[2]\i[93]\i[77]\i[21]\i[80]Last time didn't go so well."
		}
		elseif reference2 == "FLUX" || reference2 == "flele" //TODO: check if flele is actually correct
		{
			"\s[0]Oh, %(reference0) looks like they can play music. \w8\w3...I have a music player too, %(username)! \w2With cool playlists! \w5Am I not enough for you? Why do you need another music player in your life?! I WANT TO BE THE ONLY ONE WHO PLAYS MUSIC FOR YOU!!!\n\n[half]\w8\w3Nah I'm just kidding. \w4Phantasia didn't expect anyone to see this so they're having fun lmao"
		}
		elseif reference2 == "Shadow the Hedgehog" || reference2 == "Among Us" 
		{
			"HOLY SHIT %(TOUPPER(reference2))???"
		}
		else
		{
			"\0\s[0]\i[91]\i[76]%(reference0), huh? \w6\0\s[11]\i[76]\i[92]How many other people do you have on here, %(username)?\e"
		}
	}
}


//Literal suffering. Even making the florpus shell hurt me. Whoever makes a shell for anything, you have my greatest respect.

//deleting OnShellChanging because ehhhhhhhhhhhh it's like. fodder that I don't have dialogue ideas for.
 
 
 //--OnShellChanged
 
 //The sister to the above, this is the dialogue after you've changed the shell.
 
OnShellChanged
 {
	_old = nowshell
	nowshell = reference0 //Sets nowshell to the new value. reference0 is the name set in descript.txt.
	--
	if reference0 == _old && firstboot != 1 //hopefully setting a default shell on firstboot shouldnt mess this up now
	{
		"\0\s[0]\i[76]%(dibarmfiddle)...Huh? \w5Did you do something?\e"
		"\0\s[0]\i[79]\i[23]%(dibarmfiddle)...Woah. \w8\w8\w8\i[21]What was that..?\e"
	}
	elseif reference0 == "Shooting Star" //Again, this is the name of your shell as you put in the descript.txt file for that shell.
	{
		if firstboot == 1
		{
		"\0\![set,scaling,66]\_q(Due to this shell's size, Dib will be scaled down for now. The next time you boot him, though, he'll be really big! You can change shell scaling in the right click menu.)\_q\x"
		--
		OnFirstBootTalk
		}
		else
		{
		"\0\s[2]\i[91]Back to the good ol' Shooting Star. \w4\0\s[0]\i[76]\i[26]\i[91]How many people actually read that thing?\e"
		"\0\s[2]\i[91]Nice! \w8\0\s[2]\i[91]\i[73]This is exactly the size my head should be!\e"
		"\0\s[2]\i[78]\i[92]With this height, I could probably kick ZIM into the stratosphere.\e"
		}
	}
	elseif reference0 == "Florpus" //REMEMBER THIS IS CASE SENSITIVE OMFG
	{
		if firstboot == 1
		{
		OnFirstBootTalk
		}
		else
		{
		"\0\s[2]\i[91]Slipping into something more familiar.\e"
		"\0\s[3]\i[22]\i[77]\i[96]Aw, \w2now my head's bigger...\e"		
		}
	}
	else //For any shell without a specific name
	{
		if firstboot == 1
		{
		"\0...Strange, you shouldn't be able to see this dialogue. But I'm putting it here just in case something goes wrong! Report this to Phantasia, huh?\x"
		--
		OnFirstBootTalk
		}
		else
		{
		"\0\s[2]\i[91]Changed to %(reference[0]). \w4A new look!\e"
		}
	}
 }


 //--OnDressupChanged
 
 //This runs whenever you fiddle with their clothing via the dress-up option, if you've set any for your ghost. Make sure to read the clothing page on the walkthrough for details on how clothes work! This template won't have any because Girl and Triangle move around too much for it, but if your ghost is mostly stationary, clothes are an option. In that case, these functions are for you. For now, I'll fill this in with some hypothetical items. Replace the dialogue below with whatever you need.

OnDressupChanged
{
if reference0 == 0 //in OnDressupChanged, reference0 refers to the character, therefore this is evaluating which character in question is getting dressed up. 0 is the main character, as you know from the walkthrough by now.
	{
	if reference1 == "Glasses" //This is the name of the clothing item in the descript.txt file, I'm pretty sure.
		{
			if reference2 == 1 //If you put it on
			{
				"\0\s[11]Oh- \w6Finally. \w8\0\s[0]\i[70]\i[76]\i[91]Do you really think that's funny?\e"
			}
			else //When you take it off.
			{
				"\0\s[13]\i[43]What- \w8\w4\0\s[13]\i[78]\i[91]Hey! \w4Give that back!"
				--
				if nowshell == "Florpus"
				{
					"\0\s[3]\i[78]\i[91]\i[65]\w8\n\n[half](You barely notice a difference...)"
				}
				--
				"\e"
			}
		}
	}
else //if reference0 isn't the main character, then it has to be the side character.
	{
	if reference1 == "Gameslave"
		{
			if reference2 == 1 
			{
			"\1\s[10]She still looks annoyed, just... Less so.\e"
			}
			else
			{
			"\1\s[10]...\e"
			}
		}
	elseif reference1 == "Glasses"
		{
			if reference2 == 1
			{
			"\1\s[10]...\w8\0\s[0]Gaz! You got my glasses back! \w8\1\s[10]\n\n[half]Huh? No, these are mine. \w2\0\s[0]\n\n[half]Gaz!!\e"
			}
			else
			{
			"\1\s[10]She doesn't seem to have noticed you taking them.\e"
			}
		}
	}
}

//thank you zi for coding literally everything in here. i um. i don't understand any of this
OnNotifyDressupInfo
{
    _reference = reference
    
    currentdressups = "" //This one is so we can display which dressups are currently in use
    
    foreach _reference; _dressup
    {
        for _i = 3; _i < ARRAYSIZE(_dressup); _i++
        {
            if _dressup[_i] == "default" || _dressup[_i] == "" || _dressup[_i] == "mustselect" || _dressup[_i] == "multiple"; continue
            
            if _dressup[_i] == "1"
            {
                if currentdressups != ""; currentdressups += ","
                currentdressups += _dressup[1] + "|" + _dressup[2]
            }
        }
    }
}
//every time it loads or the dressups change you should get a list of all the current dressups. They're formatted like so: "Category|Piece,Category|Piece"
//So you can do a search for, say, if "Hair|Braid" _in_ currentdressups
