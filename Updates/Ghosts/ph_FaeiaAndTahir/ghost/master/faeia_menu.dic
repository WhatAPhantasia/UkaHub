SakuraMenuGreetings : nonoverlap
{
	"Hey hey~! \wNeed me for something?"
}

OnSakuraMenu : all
{
	"\0\s[1]\b2\![set,autoscroll,disable]"
	if SHIORI3FW.Eventid == "OnMouseDoubleClick"; SakuraMenuGreetings //The check here is so they won't comment if you click the back button. Remove if you prefer
	
	"\n\n\_q"
	
	"\![*]\q[Talk to me,OnAiTalk]"
	if LastTalk != ""; "  \![*]\q[Say that again,OnLastTalk]"
	"\n\n"

	"\![*]\q[Ask Questions,OnFQuestionMenu]\n\n"

	"\![*]\q[Open Spellbook,OnSpellbook]\n"
	"\![*]\q[Let's Battle!,OnFaeiaBattle]\n\n"

	"\![*]\q[Change Settings,OnSakuraSettings]\n\n"
	
	"\![*]\q[Nevermind,blank]"
}

OnLastTalk
{
	LastTalk
}

//—————————————————————————————— Master Settings ——————————————————————————————

OnSakuraSettings : all
{
	"\0\b2\![set,autoscroll,disable]\_q"
	"\f[align,center]\f[bold,1]--MASTER SETTINGS--\n\n\f[align,default]\f[bold,default]"

	"\![*]Your name is \_a[OnName,0]%(user[0])\_a.\n"
	"\![*]Faeia calls you \_a[OnNickname,0]%(user[1])\_a.\n"
	"\![*]Your pronouns: \_a[OnPronouns,0]%(Capitalize(they))/%(Capitalize(them))\_a\n\n"
	
	"\![*]To them, "
	if userinfo[0] == "unknown"; "they don't know\nwhat \_a[OnSpecies,0]species\_a you are."
	else; "you are\n%(vwl(userinfo[0],'a','an')) %(userinfo[0])."
	"\n"
	
	"\![*]They also think\nyou're %(vwl(userinfo[1],'a','an')) \_a[OnClass,0]%(userinfo[1])\_a\n"
	"with %(vwl(userinfo[2],'a','an')) \_a[OnResonance,0]%(userinfo[2])\_a resonance.\n\n"

	"\![*]\q[Change talk rate,OnTalkRateMenu,0]\n"
	"\![*]\q[RESET,OnResetConfirm,0]\n\n"
	
	"\![*]\q[Back,OnSakuraMenu]  \![*]\q[Nevermind,blank]"
}

OnResetConfirm : all
{
	_settings = "OnSakuraSettings"; if reference0; _settings = "OnKeroSettings"
	"\%(reference0)"

	"\_q\f[align,center](Are you sure? This will remove all your progress.)\n\n"

	"\![*]\q[YES,OnResetYes]   \![*]\q[NO,%(_settings)]"
}

OnTalkRateMenu : all
{	
	_settings = "OnSakuraSettings"; if reference0 == 1; _settings = "OnKeroSettings"
	"\%(reference0)"

	//This is just setting up a label, you may need to add to it or tweak it if you change what rates are available
	_talkrate = "Off"
	if aitalkinterval == 30; _talkrate = "30 seconds"
	elseif aitalkinterval == 60; _talkrate = "1 minute"
	elseif aitalkinterval == 180; _talkrate = "3 minutes"
	elseif aitalkinterval == 300; _talkrate = "5 minutes"
	elseif aitalkinterval == 600; _talkrate = "10 minutes"
	
	"\0\s[0]\b2\![set,autoscroll,disable]\_q"
	"Current interval: %(_talkrate)\n\n"
	
	"\![*]\q[Off,OnTalkRateChange,0,%(reference0)]\n"
	"\![*]\q[30 seconds,OnTalkRateChange,30,%(reference0)]\n"
	"\![*]\q[1 minute,OnTalkRateChange,60,%(reference0)]\n"
	"\![*]\q[3 minutes,OnTalkRateChange,180,%(reference0)]\n"
	"\![*]\q[5 minutes,OnTalkRateChange,300,%(reference0)]\n"
	"\![*]\q[10 minutes,OnTalkRateChange,600,%(reference0)]\n"
	"\n"
	"\![*]\q[Back,%(_settings)]  \![*]\q[Nevermind,blank]"
}

OnTalkRateChange
{
	aitalkinterval = TOINT(reference0)

	if reference1 == 0; OnSakuraSettings
	else; OnKeroSettings
}