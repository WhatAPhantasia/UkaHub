//This file handles a couple little QOL functions you may want. But these are not necessary! If you don't care for them just erase them. If you don't want to set up network updates, you can comment or remove the update functions too. If you're not interested in any of these, just remove the whole file!

//—————————————————————————————— Installing something ——————————————————————————————
OnInstallComplete
{
	"\1\s[11]\0\s[1](Installed a %(reference0),\w4 called %(reference1).)"
	--
	if "ghost" _in_ reference0 //A little menu to open or change to the ghost that was installed immediately
	{
		"\n\n\_q"
		--
		"\![*]\__q[OnInstallComplete.Action,call]Call %(reference0)\__q  \![*]\__q[OnInstallComplete.Action,change]Change to %(reference0)\__q\n\n"
		--
		"\![*]\q[Thanks,blank]"
	}
}

OnInstallComplete.Action
{
	if reference0 == "change"
	{
		"\![change,ghost,lastinstalled]"
	}
	else //call
	{
		"\![call,ghost,lastinstalled]"
	}
}


//—————————————————————————————— Creating a .nar file ——————————————————————————————
OnNarCreated
{
	_nar = "\_a[OnNarLocation,""%(reference1)""]%(reference0)\_a" //Makes a clickable link that'll open the location of the file they just made
	
	"\1\s[11]\0\s[1](Successfully created %(_nar).)"
}

OnNarLocation
{	
	_path = SPLITPATH(reference0)[0] + SPLITPATH(reference0)[1]
	
	"\![open,file,""%(_path)""]"
}


//—————————————————————————————— Network Update ——————————————————————————————
//Starting an update
OnUpdateBegin
{
	"\1\s[11]\i[32]\0\s[1]Ooh, is that a carrier rune..?! \w8\i[35]I wonder if we have any cool updates...\w8"
}

//reference0 is the number of new files, starting from 0
OnUpdateReady
{
	reference0++ //Number of files starts from 0, so this displays the correct amount
	
	//plural checks, adds an s and changes is to are if there's more than 1 file
	_s = ""; _are = "is"; if reference0 != 1; _s = "s"; _are = "are"
	
	"\1\s[11]\0\s[1]Looks like %(reference0) new file%(_s)! \w8\i[34]Alright, give me a sec to read through this...\w8"
}

//When the update finishes. Don't forget to initialize any new variables you've created! I highly highly recommend using OnInitialize for this (it's in boot.dic), in case the user updates via the ghost explorer or some other means.
//reference0 is 'none' if there were no new files to update with, reference1 is a list of new file names, separated by commas
OnUpdateComplete
{
	if reference0 == "none"
	{
		"\1\s[11]\i[32]\0\s[1]\i[35]\i[62]Aw, there's nothing new...\w8\w8\1\s[11]\i[12]\i[35]\i[62]I was actually kind of hoping for a letter..."
	}
	else
	{
		"\1\s[11]\i[32]\0\s[1]Done!! \w8\i[35]Yaay, we got some new stuff in the mail~!\e"
	}
}

//reference0 is the reason the update failed
OnUpdateFailure //If the update fails
{
	if reference0 == "timeout" //If it timed out
	{
		"\1\s[11]\i[32]\0\s[6]Aw, what?! The mail's gone...\w8\w8\1I think the rune timed out or something..."
	}
	elseif reference0 == "md5 miss" //If the update files have not been uploaded correctly and don't match the updates2.dau
	{
		_file = SPLITPATH(reference1)[2] + SPLITPATH(reference1)[3] //This gets the name and extension of the offending file
		
		"\1\s[11]\i[32]\0\s[1]\i[34]\i[62]Aw, looks like the MD5 values don't match.\w8\w8\n\n\_q(Contact Phantasia and let them know that '%(_file)' is giving an MD5 error.)"
	}
	elseif reference0 == "artificial" //If the user cancels the update by double clicking the balloon
	{
		"\1\s[11]\i[32]\0\s[1]\i[14]Wait, huh..?! \w8\![anim,stop,14]\i[13]\i[34]\i[62]Guess there's no mail for us after all..."
	}
	else //Any other errors
	{
		"\1\s[11]\i[32]\0\s[6]Aw, what?! \w8Something went wrong, I think..? \w8\s[1]\i[14]It just says, um...\w4 ""%(reference0)""."
	}
}