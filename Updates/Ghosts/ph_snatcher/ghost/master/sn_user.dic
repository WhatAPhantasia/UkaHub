//---------------------------User Info!--------------------------

//SNATCHER YEAHHHHHHHHHH


OnNameTeach
{
	//Taken from x template! thank u so much zi u are so smart
	_tempname = CUTSPACE(reference.raw[0]) //this stores the original name in _tempname
	_name = reference.raw[0] //putting the raw name into _name to do some tests on it
	_name = TOLOWER(_name) //changes it to lowercase
	_name = REPLACE(_name," ","") //removes spaces
	_name = REPLACE(_name,".","") //removes periods
	_name = REPLACE(_name,"-","") //removes dashes
	
	_is_first_boot = 0; if reference1 == "firstboot"; _is_first_boot = 1
	_function = "OnNameTeach"; if _is_first_boot; _function = "OnFirstNameTeach"
	
	if _is_first_boot; "\_l[-10000,-10000]\q[ ,__VERY_FIRST_BOOT__]\_l[0,0]\*\t" //Prevents balloon from timing out if this is during first boot. Also prevents the user from closing out the input box
	
	if ("snatcher" _in_ _name)
	{
		if foundinnocent <= 1
		{
		"\0\s[0]Oh, \f[bold,1]very\f[bold,0] funny. \w4Steal my stuff, \w1my minions, \w1me, \w1and now \w1my name. \w6You can never be the real deal, kid!\![open,inputbox,OnNameTeach,-1]"
		}
		else
		{
		"\0\s[0]Nope, that name isn't for you, kiddo.\w8\![open,inputbox,OnNameTeach,-1]"
		}
	}
	elseif _name == "vanessa"
	{
		username = SHIORI3FW.EscapeAllTags(_tempname)
		"\0\s[0]...Ah. w6What a lovely name. \w6\x%(namedone)"
	}
	elseif _name == "queenvanessa" || _name == "moonjumper" || _name == "hatkid" || _name == "bowkid" || _name == "mustachegirl" || _name == "conductor" || _name == "theconductor" || _name == "djgrooves" || _name == "restingking" || _name == "mafiaboss"
	{
		"\0\s[0]\i[1]\b[4]That name already belongs to someone, and stealing is wrong! Try again.\w8\![open,inputbox,OnNameTeach,-1]"
		//TODO: check surface where it cancels snatcher's talking anim
		//TODO: make unique dialogues for some of these
	}
	elseif _name == "timmy" || _name == "minion" || _name == "kid" || _name == "darling" || _name == "love"
	{
		username = SHIORI3FW.EscapeAllTags(_tempname)
		"\0\s[0]Pfft- Hahaha, okay, sure. \w6\x%(namedone)"
	}
	elseif _name == "eshal" || _name == "phantasia" || _name == "whataphantasia"
	{
		username = SHIORI3FW.EscapeAllTags(_tempname)
		"\0\s[0]Hahahaha! Looking for secrets, are we? \w6\x%(namedone)"
	}
	elseif reference0 == "" 
	{
		username = "kid"
		
		"\0\s[0]Huh? What's that? At a loss for words? \w6\x%(namedone)"
	}
	else
	{
		username = SHIORI3FW.EscapeAllTags(_tempname)
	}

}
namedone
{
	if firstboot == 1
	{		
		"\0\s[0]\i[1]\b[4]And what are your pronouns?"
		--
		"\w5\n\n\_q\![*]\q[He/Him,choicemasc]   \![*]\q[She/Her,choicefem]   \![*]\q[They/Them,choiceneither]\e"
		//TODO: check \i[1] where it cancels snatcher's talking anim
	}
	else
	{
		"\0\s[0]Alright, your name is %(username) now. \w4You should be grateful legalities are so lenient here. \x\e"
	}
}
	
OnChangePronoun
{
	"\0\s[0]\i[1]\b[4]Choose your pronouns!\e"
	--
	"\w5\_q\![*]\q[He/Him,choicemasc]\n\![*]\q[She/Her,choicefem]\n\![*]\q[They/Them,choiceneither]\e"
}

Select.choicemasc
{
	presuffix = "masculine"
	their = "his"
	theirs = "his"
	they = "he"
	them = "him"
	theyre = "he's"
	theywere = "he was"
	--
	pronoundone
}

Select.choicefem
{
	presuffix = "feminine"
	their = "her"
	theirs = "hers"
	they = "she"
	them = "her"
	theyre = "she's"
	theywere = "she was"
	--
	pronoundone
}

Select.choiceneither
{
	presuffix = "undefine"
	their = "their"
	theirs = "theirs"
	they = "they"
	them = "them"
	theyre = "they're"
	theywere = "they were"
	--
	pronoundone
}
//planning on adding neos.....


pronoundone
{
	if firstboot == 1
	{
		firstboot = 0
		--
		//^ might be redundant but oh well
		"\![leave,passivemode]"
		--
		if soulcustody == 0 && foundinnocent != 2
		{
			"\0\s[0]AHAHAHHA! That seals the deal! Alright, how do I get your soul from here... \w6\x"
			--
			soulcustody = 1
			--
			"\i[1]\b[4]You feel exceedingly empty inside. \w6\x\s[0]Another braindead servant to add to the pile! \w5And, \w2actually, \w2your place here is pretty neat... \w6\xIt doesn't look like I can get out of here by myself, but that just means more quality time with the guy you decided to steal from, huh? \w6I'll be looking for the stuff you stole. \w6\x\i[1]\b[4]You... \w6You really didn't do any of that. \w6Maybe you can \f[bold,1]ask\f[bold,0] him about the details on the whole stealing disaster. \w6\x\i[1]\b[4]Or \w3you can just let him hang around. \w5He sounds fun.\w8\w2\x\e"
		}
		elseif soulcustody == 0 //&& foundinnocent == 2 (omitted bc its redundant but maybe i'll need this who knows)
		{
			"\0\s[0]Wonderful to see you again, %(username). \w6And, \w4hey, \w6if you ever want to give me your soul again, I really wouldn't mind... \x\e"
		}
		else
		{
			"\0\s[0]That seals the deal! \w3\f[bold,1]So\f[bold,0] \w6good to see you again, %(username). \x\e"
		}
	}
	else
	{
		"\0\s[0]Oh? I'll keep it in mind. \x\e"
	}
}


//Change this later, just taking from x template so i can get this ghost done in time lmao------------
OnChangeBirthday //Pulled a lot of this from the GT template, but I also modified it a lot
{
	oldbirthday = birthday
	
	"\0\s[0]Alright, sure, \w3humor me. \w8\w8Name or number of the month, \w2go on."
	--
	"\n\n\![*]\q[Cancel,OnConfigMenu,OnUserBornMonth]\![open,inputbox,OnUserBornMonth,-1]\![set,balloontimeout,-1]\e"
}

//birthday[0] - word birthday
//birthday[1] - number birthday
//birthday[2] - word month
//birthday[3] - word day
//birthday[4] - number month
//birthday[5] - number day
//Use birthday[0] to display the user's birthday, and birthday[1] to do checks with the numbers

OnUserBornMonth
{
	_tempmonth = reference0 //this stores what the user put in as a variable called _tempmonth
	_tempmonth = TOLOWER(_tempmonth) //changes it to lowercase
	
	if _tempmonth == "january" || _tempmonth == "1" {birthday[4] = 1; birthday[2] = "January"}
	elseif _tempmonth == "february" || _tempmonth == "2" {birthday[4] = 2; birthday[2] = "February"}
	elseif _tempmonth == "march" || _tempmonth == "3" {birthday[4] = 3; birthday[2] = "March"}
	elseif _tempmonth == "april" || _tempmonth == "4" {birthday[4] = 4; birthday[2] = "April"}
	elseif _tempmonth == "may" || _tempmonth == "5" {birthday[4] = 5; birthday[2] = "May"}
	elseif _tempmonth == "june" || _tempmonth == "6" {birthday[4] = 6; birthday[2] = "June"}
	elseif _tempmonth == "july" || _tempmonth == "7" {birthday[4] = 7; birthday[2] = "July"}
	elseif _tempmonth == "august" || _tempmonth == "8" {birthday[4] = 8; birthday[2] = "August"}
	elseif _tempmonth == "september" || _tempmonth == "9" {birthday[4] = 9; birthday[2] = "September"}
	elseif _tempmonth == "october" || _tempmonth == "10" {birthday[4] = 10; birthday[2] = "October"}
	elseif _tempmonth == "november" || _tempmonth == "11" {birthday[4] = 11; birthday[2] = "November"}
	elseif _tempmonth == "december" || _tempmonth == "12" {birthday[4] = 12; birthday[2] = "December"}
	else //if the month isn't a valid month, like 32 or something.
	{
		"\0\s[0]%(_tempmonth)? Nope, can't have that as a month."
		--
		"\n\n\![*]\q[Cancel,OnConfigMenu,OnUserBornMonth]\![open,inputbox,OnUserBornMonth,-1]\![set,balloontimeout,-1]\*\e"
	}
	--
	//If it's a valid month
	"%(birthday[2]) huh?\w8\w8 Ok,\w4 what day?"
	--
	"\n\n\![*]\q[Cancel,OnConfigMenu,OnUserBornDay]\![open,inputbox,OnUserBornDay,-1]\![set,balloontimeout,-1]\*\e"
}

OnUserBornDay
{
	
	_tempday = reference0 //storing the input in _tempday
	_tempday = TOLOWER(_tempday) //changes it to lowercase
	_tempday = REPLACE(_tempday,"st","") //if the user types something like 2nd, 3rd, etc, this will make it just the number. It'll just add the suffix back later on the one that actually shows.
	_tempday = REPLACE(_tempday,"nd","")
	_tempday = REPLACE(_tempday,"rd","")
	_tempday = REPLACE(_tempday,"th","")
	_tempday = TOINT(_tempday) //Converts it to an integer
	_month = birthday[4] //Just for readability
	
	if ((_month == 1 || _month == 3 || _month == 5 || _month == 7 || _month == 8 || _month == 10 || _month == 12) && _tempday >= 1 && _tempday <= 31) || (_month == 2 && _tempday >= 1 && _tempday <= 29) || ((_month == 4 || _month == 6 || _month == 9 || _month == 11) && _tempday >= 1 && _tempday <= 30) //This ugly function checks if the day falls into a valid range.
	{
		birthday[5] = _tempday
		birthday[3] = Printday
		birthday[1] = "%(birthday[4])/%(birthday[5])" //If you want to write dates as DD/MM, feel free to swap this around
		birthday[0] = "%(birthday[2]) %(birthday[3])"
		ERASEVAR("oldbirthday")
		
		if birthday[1] == "%(month)/%(day)" //if today is the user's birthday. Don't forget to switch this if you change the format to DD/MM
		{
			"\0\s[0]Today? \w8Sure, \w2okay, \w2I believe you."
		}
		elseif birthday[1] == "12/25" //If it's on christmas. Put any date you'd like here. If you change the format to DD/MM, make sure you change any dates like this that might be in bootend.
		{
			"\0\s[0]Oh,\w4 you were born on Christmas? \w4Hm."
		}
		else //if the birthday is not listed above.
		{
			"\0\s[0]Your birthday is %(birthday[0])...\w8\w8 I probably won't remember."
		}
		--
		"\n\n\![*]\q[Back to config,OnConfigMenu]\n\![*]\q[I'm done,cancelnodialogue]\e"
	}
	else //If it isn't a valid day, we open the input box again and have a \e so the dialogue below doesn't play.
	{
		"\0\s[0]Hm, no, that isn't a good day. Try again."
		--
		"\n\n\![*]\q[Cancel,OnConfigMenu,OnUserBornDay]\![open,inputbox,OnUserBornDay,-1]\*\e"
	}
	
}

Printday //Sets the appropriate suffix
{
	if birthday[5] == 1 || birthday[5] == 21 || birthday[5] == 31
	{
		"%(birthday[5])st"
	}
	elseif birthday[5] == 2 || birthday[5] == 22
	{
		"%(birthday[5])nd"
	}
	elseif birthday[5] == 3 || birthday[5] == 23
	{
		"%(birthday[5])rd"
	}
	else
	{
		"%(birthday[5])th"
	}
}